
#Область ПрограммныйИнтерфейс

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ)
	
	Если КонтактнаяИнформация.Количество() > 0 Тогда
		
		Телефоны 	= "";
		Email		= "";
		
		//Данных не так много, проще сразу пробежаться по ТЧ, чем поиском или запросом.
		Для каждого СтрокаТЧ Из КонтактнаяИнформация Цикл
			Если СтрокаТЧ.Вид = Справочники.ВидыКонтактнойИнформации.EmailКлиента Тогда	
				Если ПустаяСтрока(СтрокаТЧ.Представление) Тогда
					Продолжить;	
				КонецЕсли;
				Email = Email + СтрокаТЧ.Представление + ";"; 
			ИначеЕсли СтрокаТЧ.Вид = Справочники.ВидыКонтактнойИнформации.ТелефонКлиента 
				ИЛИ СтрокаТЧ.Вид = Справочники.ВидыКонтактнойИнформации.ОсновнойТелефонКлиента Тогда
				Если ПустаяСтрока(СтрокаТЧ.НомерТелефона) Тогда
					Продолжить;	
				КонецЕсли;	
				Телефоны = Телефоны + СтрокаТЧ.НомерТелефона + ";";
			КонецЕсли;	
		КонецЦикла;
		
		//Если НЕ ПустаяСтрока(Телефоны) ИЛИ НЕ ПустаяСтрока(Email) Тогда
			
			НаборЗаписей = РегистрыСведений.КлиентыКонтактнаяИнформация.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Клиент.Установить(Ссылка);
			
			НоваяЗапись 			= НаборЗаписей.Добавить();
			НоваяЗапись.Клиент 		= Ссылка;
			НоваяЗапись.Телефоны	= Телефоны;
			НоваяЗапись.Email		= Email;
			
			НаборЗаписей.Записать();
				
		//КонецЕсли;
	Иначе
		НаборЗаписей = РегистрыСведений.КлиентыКонтактнаяИнформация.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Клиент.Установить(Ссылка);
		НаборЗаписей.Записать();
	КонецЕсли;
	
	Если НЕ Отказ 
			И ДополнительныеСвойства.НеобходимаКорректировкаЗаписейИсключенияДублей Тогда
			
		НаборЗаписей = РегистрыСведений.ИсключенияДублированияКлиентов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Клиент.Установить(Ссылка);
		НаборЗаписей.Записать();
	
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ЭтоНовый() Тогда
		ДатаСозданияЭлемента = ТекущаяДата();			
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("НеобходимаКорректировкаЗаписейИсключенияДублей", НеобходимаКорректировкаЗаписейИсключенияДублей());
	
	//<821883>, Басаргин (28.05.2018) {
	Для каждого СтрокаТабличнойЧасти Из КонтактнаяИнформация Цикл					
		Если НЕ СтрокаТабличнойЧасти.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон
			И (СтрокаТабличнойЧасти.Вид = Справочники.ВидыКонтактнойИнформации.ОсновнойТелефонКлиента
				ИЛИ СтрокаТабличнойЧасти.Вид = Справочники.ВидыКонтактнойИнформации.ТелефонКлиента) Тогда
			Продолжить;	
		КонецЕсли;				
		Результат = СтрокаТабличнойЧасти.НомерТелефона;		
		ПервыйСимвол = Лев(Результат, 1);
		НачальныеСимволы = Лев(Результат, 2);
		//Если (СтрДлина(Результат) = 11 И (ПервыйСимвол = "8" ИЛИ ПервыйСимвол = "7")) 
		//	ИЛИ (СтрДлина(Результат) = 12 И (НачальныеСимволы = "+7")) Тогда
			Если НЕ СтрокаТабличнойЧасти.НомерТелефонаБезКодов = Прав(Результат, 7) Тогда
				СтрокаТабличнойЧасти.НомерТелефонаБезКодов = Прав(Результат, 7);				
				КонтактнаяИнформацияXDTO = УправлениеКонтактнойИнформациейСлужебный.КонтактнаяИнформацияXDTOПоПредставлению(СтрокаТабличнойЧасти.Представление, СтрокаТабличнойЧасти.Вид);
				СтрокаТабличнойЧасти.ЗначенияПолей = УправлениеКонтактнойИнформациейСлужебный.КонтактнаяИнформацияXDTOВXML(КонтактнаяИнформацияXDTO);
			КонецЕсли;
		//КонецЕсли;	
	КонецЦикла;
	//<821883> }
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НеобходимаКорректировкаЗаписейИсключенияДублей()
	
	Результат = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КлиентыКонтактнаяИнформация.НомерТелефонаБезКодов КАК НомерТелефонаБезКодов
	|ПОМЕСТИТЬ вт_ДанныеСсылки
	|ИЗ
	|	Справочник.Клиенты.КонтактнаяИнформация КАК КлиентыКонтактнаяИнформация
	|ГДЕ
	|	КлиентыКонтактнаяИнформация.НомерТелефонаБезКодов <> """"
	|	И КлиентыКонтактнаяИнформация.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КИ.НомерТелефонаБезКодов КАК НомерТелефонаБезКодов
	|ПОМЕСТИТЬ вт_ДанныеОбъекта
	|ИЗ
	|	&КИ КАК КИ
	|ГДЕ
	|	КИ.НомерТелефона <> """"
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_ДанныеОбъекта.НомерТелефонаБезКодов,
	|	вт_ДанныеСсылки.НомерТелефонаБезКодов
	|ИЗ
	|	вт_ДанныеСсылки КАК вт_ДанныеСсылки
	|		ПОЛНОЕ СОЕДИНЕНИЕ вт_ДанныеОбъекта КАК вт_ДанныеОбъекта
	|		ПО вт_ДанныеСсылки.НомерТелефонаБезКодов = вт_ДанныеОбъекта.НомерТелефонаБезКодов
	|ГДЕ
	|	вт_ДанныеОбъекта.НомерТелефонаБезКодов ЕСТЬ NULL ИЛИ вт_ДанныеСсылки.НомерТелефонаБезКодов ЕСТЬ NULL";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("КИ",		КонтактнаяИнформация.Выгрузить());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти


