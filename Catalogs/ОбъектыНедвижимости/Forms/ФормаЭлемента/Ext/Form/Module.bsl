
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Квартирограмма.ГруппаПравообладателя КАК ГруппаПравообладателя,
	|	Квартирограмма.КоличествоКомнат КАК КоличествоКомнат,
	|	Квартирограмма.Корпус КАК Корпус,
	|	Квартирограмма.НомерНаПлощадке КАК НомерНаПлощадке,
	|	Квартирограмма.Площадь КАК Площадь,
	|	Квартирограмма.Подъезд КАК Подъезд,
	|	Квартирограмма.ТипОтделки КАК ТипОтделки,
	|	Квартирограмма.Этаж КАК Этаж,
	|	СтатусыОбъектовНедвижимостиСрезПоследних.Статус КАК Статус,
	|	Квартирограмма.РасшифровкаСтатуса КАК РасшифровкаСтатуса,
	|	Квартирограмма.ОбъектСтроительства.Фаза КАК Фаза,
	|	Квартирограмма.ЦенаОтделкиЗаМетр КАК ЦенаОтделкиЗаМетр,
	|	Квартирограмма.Стоимость КАК Стоимость,
	|	Квартирограмма.ЦенаЗаМетр КАК ЦенаЗаМетр,
	|	Квартирограмма.Стоимость - Квартирограмма.ЦенаОтделкиЗаМетр * Квартирограмма.Площадь КАК СтоимостьБезОтделки,
	|	Квартирограмма.МаксимальнаяСкидка КАК МаксимальнаяСкидка,
	|	Квартирограмма.ДатаСтартаПродаж КАК ДатаСтартаПродаж,
	|	Квартирограмма.СтатусПула КАК СтатусПула,
	|	Квартирограмма.СтроительныйНомер КАК СтроительныйНомер,
	|	Квартирограмма.ПлощадьПроектная КАК ПлощадьПроектная
	|ИЗ
	|	РегистрСведений.Квартирограмма КАК Квартирограмма
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыОбъектовНедвижимости.СрезПоследних(, ОбъектНедвижимости = &ОбъектНедвижимости) КАК СтатусыОбъектовНедвижимостиСрезПоследних
	|		ПО Квартирограмма.ОбъектНедвижимости = СтатусыОбъектовНедвижимостиСрезПоследних.ОбъектНедвижимости
	|ГДЕ
	|	Квартирограмма.ОбъектНедвижимости = &ОбъектНедвижимости";
	
	Запрос.УстановитьПараметр("ОбъектНедвижимости", Объект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(ЭтаФорма, Выборка);				
	КонецЕсли; 
			
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьПланировкуКвартиры(Команда)
		
	Если ЗначениеЗаполнено(Объект.ПланировкаКвартиры) Тогда
		ПараметрыОткрываемойФормы = Новый Структура("Объект,ЗаголовокФормы,ПланировкаЭтажа", Объект, "Планировка квартиры для ОН: " + Объект.Наименование, Ложь);
		ОткрытьФорму("Справочник.ОбъектыНедвижимости.Форма.ФормаПланировки", ПараметрыОткрываемойФормы, ЭтаФорма);	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПланировкуЭтажа(Команда)
	
	Если ЗначениеЗаполнено(Объект.ПланировкаСекции) Тогда
		ПараметрыОткрываемойФормы = Новый Структура("Объект,ЗаголовокФормы,ПланировкаЭтажа", Объект, "Планировка этажа для ОН: " + Объект.Наименование, Истина);
		ОткрытьФорму("Справочник.ОбъектыНедвижимости.Форма.ФормаПланировки", ПараметрыОткрываемойФормы, ЭтаФорма);	
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Бронирования(Команда)

	ПараметрыОткрываемойФормы = Новый Структура("Ключ, ОбъектНедвижимости, Заголовок", Объект, Объект, "Очередь бронирования по ОН: " + Строка(Объект));
	ОткрытьФорму("Справочник.ОбъектыНедвижимости.Форма.ФормаОчередиБронирования", ПараметрыОткрываемойФормы, ЭтаФорма);	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти