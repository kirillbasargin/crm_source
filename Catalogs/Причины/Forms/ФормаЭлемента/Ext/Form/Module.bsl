&НаКлиенте
Перем МаксКлюч;

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПричиныПодарки.Номенклатура,
	|	ПричиныПодарки.Проект,
	|	ПричиныПодарки.Номенклатура КАК Количество
	|ПОМЕСТИТЬ вт_ИсходнаяТаблица
	|ИЗ
	|	&Подарки КАК ПричиныПодарки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_ИсходнаяТаблица.Номенклатура,
	|	вт_ИсходнаяТаблица.Проект,
	|	КОЛИЧЕСТВО(вт_ИсходнаяТаблица.Количество) КАК Количество
	|ИЗ
	|	вт_ИсходнаяТаблица КАК вт_ИсходнаяТаблица
	|
	|СГРУППИРОВАТЬ ПО
	|	вт_ИсходнаяТаблица.Номенклатура,
	|	вт_ИсходнаяТаблица.Проект
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(вт_ИсходнаяТаблица.Количество) > 1";
	
	Запрос.УстановитьПараметр("Подарки", Объект.Подарки.Выгрузить());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Отказ = Истина;
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Сообщение 		= Новый СообщениеПользователю();
			Текст 			= "Обнаружен дубль номенклутары: """ + Выборка.Номенклатура + """ по проекту """ + Выборка.Проект + """ его необходимо удалить из табличной части!";
			Сообщение.Текст = Текст;
			Сообщение.УстановитьДанные(ТекущийОбъект.Ссылка);
			Сообщение.Сообщить();
			
		КонецЦикла;  
		
	КонецЕсли; 
		
КонецПроцедуры

&НаКлиенте
Процедура ПодаркиПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("УстановитьОтборВОбъектах",0.3,Истина);
		
КонецПроцедуры

&НаКлиенте
Процедура ПодаркиПередУдалением(Элемент, Отказ)
	// Необходимо очистить дополнительные сведения
	КлючПоиска = Новый Структура("КлючСтроки" , Элементы.Подарки.ТекущиеДанные.КлючСтроки);
	
	УдалитьСвязанныеЗаписи(КлючПоиска, Отказ);

КонецПроцедуры

&НаКлиенте
Процедура УдалитьСвязанныеЗаписи(КлючПоиска, Отказ)
	
	НайденныеСтрокиОбъекты = Объект.Объекты.НайтиСтроки(КлючПоиска);
	
	Если НайденныеСтрокиОбъекты.Количество() > 0 Тогда
		Для Каждого Строка Из НайденныеСтрокиОбъекты Цикл
			Объект.Объекты.Удалить(Строка);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьНовыйКлючСтроки(ИмяТЧ = "Подарки") Экспорт

	Если МаксКлюч = 0 И Объект[ИмяТЧ].Количество() > 1  Тогда
		Для Каждого Стр Из Объект[ИмяТЧ] Цикл
			МаксКлюч = Макс(МаксКлюч, Стр.КлючСтроки);
		КонецЦикла;
	КонецЕсли;
	
	МаксКлюч = МаксКлюч + 1;
	
	
	Возврат МаксКлюч;

КонецФункции 

&НаКлиенте
Процедура ОбъектыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	 Элементы.Объекты.ТекущиеДанные.КлючСтроки = Элементы.Подарки.ТекущиеДанные.КлючСтроки;
КонецПроцедуры

&НаКлиенте
Процедура ПодаркиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда
		Элементы.Подарки.ТекущиеДанные.КлючСтроки = ПолучитьНовыйКлючСтроки();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	МаксКлюч = 0;
	
	ОтборПодарки = Новый Структура("КлючСтроки", 0);
	Элементы.Объекты.ОтборСтрок = Новый ФиксированнаяСтруктура(ОтборПодарки);

КонецПроцедуры

&НаКлиенте
Процедура ОбъектыПриИзменении(Элемент)
	
  ПодключитьОбработчикОжидания("УстановитьОтборВОбъектах",0.5,Истина);

КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборВОбъектах()
	
	Если Элементы.Подарки.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОтборПодарки = Новый Структура("КлючСтроки", 0);
	ОтборПодарки.КлючСтроки = Элементы.Подарки.ТекущиеДанные.КлючСтроки;
	Элементы.Объекты.ОтборСтрок = Новый ФиксированнаяСтруктура(ОтборПодарки);

КонецПроцедуры

&НаКлиенте
Процедура ПодаркиНоменклатураПриИзменении(Элемент)
	Элементы.Объекты.ДобавитьСтроку();
	ЭтаФорма.ТекущийЭлемент = Элементы.Объекты.ТекущийЭлемент;
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиОбъектыВТЧ(Команда)
ПеренестиНаСервере();		
УстановитьОтборВОбъектах();
КонецПроцедуры

&НаСервере
Процедура ПеренестиНаСервере()
	СпрОбъект = РеквизитФормыВЗначение("Объект");
	СпрОбъект.ПеренестиОбъектыВТЧ();
	ЗначениеВРеквизитФормы(СпрОбъект,"Объект");
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВсеФлажкиАктуальности(Команда)
	Если НЕ Элементы.Подарки.Доступность ИЛИ Элементы.Подарки.ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Стр Из Объект.Подарки Цикл
		Стр.Актуален = Истина;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсеФлажкиАктуальности(Команда)
	Если НЕ Элементы.Подарки.Доступность ИЛИ Элементы.Подарки.ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Стр Из Объект.Подарки Цикл
		Стр.Актуален = Ложь;
	КонецЦикла;

КонецПроцедуры
