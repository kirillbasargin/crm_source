#Область ПрограммныйИнтерфейс


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОбъектНедвижимостиЗабронирован(ОбъектНедвижимости, Регистратор, ЭтимЖеДокументом = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтатусыОбъектовНедвижимостиСрезПоследних.ОбъектНедвижимости КАК ОбъектНедвижимости
	|ИЗ
	|	РегистрСведений.СтатусыОбъектовНедвижимости.СрезПоследних(&Период, ОбъектНедвижимости = &ОбъектНедвижимости) КАК СтатусыОбъектовНедвижимостиСрезПоследних
	|ГДЕ
	|	СтатусыОбъектовНедвижимостиСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОбъектовНедвижимости.Забронирован)";
	Если ЭтимЖеДокументом Тогда 
		Запрос.Текст = Запрос.Текст + " И СтатусыОбъектовНедвижимостиСрезПоследних.ДокументРегистратор = &ДокументРегистратор";
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ОбъектНедвижимости", 	ОбъектНедвижимости);
	Запрос.УстановитьПараметр("ДокументРегистратор", 	Регистратор);
	Запрос.УстановитьПараметр("Период",					ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Регистратор, "Дата"));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат НЕ РезультатЗапроса.Пустой();
		
КонецФункции

Функция ПолучитьТекущийСтатусЗаявкиНаСделку(ЗаявкаНаСделку) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ Т.Статус ИЗ РегистрСведений.СтатусыСделки.СрезПоследних(, ЗаявкаНаСделку = &ЗаявкаНаСделку) КАК Т";
	Запрос.УстановитьПараметр("ЗаявкаНаСделку", ЗаявкаНаСделку);
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	Возврат Выборка.Статус;	

КонецФункции // ()

Процедура УдалитьДвиженияПоРегиструСтатусовОбъектовНедвижимости(Регистратор) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтатусыОбъектовНедвижимости.Период КАК Период,
	|	СтатусыОбъектовНедвижимости.ОбъектНедвижимости КАК ОбъектНедвижимости
	|ИЗ
	|	РегистрСведений.СтатусыОбъектовНедвижимости КАК СтатусыОбъектовНедвижимости
	|ГДЕ
	|	СтатусыОбъектовНедвижимости.ДокументРегистратор = &ДокументРегистратор";
	Запрос.УстановитьПараметр("ДокументРегистратор", Регистратор);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		НаборЗаписей = РегистрыСведений.СтатусыОбъектовНедвижимости.СоздатьНаборЗаписей();
		
		НаборЗаписей.Отбор.Период.Установить(Выборка.Период);
		НаборЗаписей.Отбор.ОбъектНедвижимости.Установить(Выборка.ОбъектНедвижимости);
		НаборЗаписей.Прочитать();
		НаборЗаписей.Очистить();	
		НаборЗаписей.Записать();	
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти