
Процедура УстановитьВидимостьДополнительныхРеквизитовСвидетельства(Форма, СпрСвидетельство) Экспорт
	
	ВидимостьРеквизитовСвидетельсвта = Форма.ДокументыФизическихЛиц.ВидДокумента = ПредопределенноеЗначение("Справочник.ВидыДокументовФизическихЛиц.СвидетельствоОРождении");
	
	Форма.Элементы.СвидетельстваОРожденииФизическихЛицНомерАкта.Видимость 			= ВидимостьРеквизитовСвидетельсвта;
	Форма.Элементы.СвидетельстваОРожденииФизическихЛицДатаАкта.Видимость 			= ВидимостьРеквизитовСвидетельсвта;
	Форма.Элементы.СвидетельстваОРожденииФизическихЛицМатьФИО.Видимость 			= ВидимостьРеквизитовСвидетельсвта;
	Форма.Элементы.СвидетельстваОРожденииФизическихЛицМатьНациональность.Видимость 	= ВидимостьРеквизитовСвидетельсвта;
	Форма.Элементы.СвидетельстваОРожденииФизическихЛицОтецФИО.Видимость 			= ВидимостьРеквизитовСвидетельсвта;
	Форма.Элементы.СвидетельстваОРожденииФизическихЛицОтецНациональность.Видимость 	= ВидимостьРеквизитовСвидетельсвта;	
	
КонецПроцедуры

Процедура УстановитьВидимостьИНН_КПП(Форма) Экспорт
	
	ВидимостьРеквизитовЮрЛица = Форма.Объект.ТипКлиента = ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо");
	
	Форма.Элементы.ИНН.Видимость 	= ВидимостьРеквизитовЮрЛица;
	Форма.Элементы.КПП.Видимость 	= ВидимостьРеквизитовЮрЛица;
	Форма.Элементы.Брокер.Видимость = НЕ ВидимостьРеквизитовЮрЛица;
	
КонецПроцедуры

// Возвращает Истина, если переданный объект содержит 
// значения по умолчанию для гражданства.
//
// Параметры:
//	ИнформацияОГражданстве - объект, имеющий свойства.
//		Страна
//
Функция ГражданствоПоУмолчанию(ИнформацияОГражданстве) Экспорт
	
	Возврат ИнформацияОГражданстве.Страна = ПредопределенноеЗначение("Справочник.СтраныМира.Россия");
	
КонецФункции

Процедура ОбновитьПолеГражданствоПериод(Форма, ДатаСеанса) Экспорт
	
	// Не обязательно заполнение поля Период если данные по умолчанию и при этом 
	// записи о гражданстве еще нет.
	Если СправочникКлиентыКлиентСервер.ГражданствоПоУмолчанию(Форма.ГражданствоФизическихЛиц)
		И (Форма.ГражданствоФизическихЛицПрежняя.Период = '18991231000000'
			ИЛИ Форма.ГражданствоФизическихЛицПрежняя.Период = '00010101') Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Форма.Элементы,
			"ГражданствоФизическихЛицПериод",
			"АвтоОтметкаНезаполненного",
			Ложь);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Форма.Элементы,
			"ГражданствоФизическихЛицПериод",
			"ОтметкаНезаполненного",
			Ложь);
			
	Иначе
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Форма.Элементы,
			"ГражданствоФизическихЛицПериод",
			"АвтоОтметкаНезаполненного",
			Истина);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Форма.Элементы,
			"ГражданствоФизическихЛиц",
			"ОтметкаНезаполненного",
			НЕ ЗначениеЗаполнено(Форма.ГражданствоФизическихЛиц.Период));
		
		Если НЕ СправочникКлиентыКлиентСервер.ГражданствоПоУмолчанию(Форма.ГражданствоФизическихЛиц) 
			И Форма.ГражданствоФизическихЛиц.Период = '18991231000000' Тогда
			
			Форма.ГражданствоФизическихЛиц.Период = НачалоДня(ДатаСеанса);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Форма.ГражданствоФизическихЛиц.Период = '18991231000000' Тогда
		Форма.ГражданствоФизическихЛицПериод = '00010101';
	Иначе
		Форма.ГражданствоФизическихЛицПериод = Форма.ГражданствоФизическихЛиц.Период;
	КонецЕсли;

КонецПроцедуры

Процедура УстановитьВидимостьПолейФИО(Форма) Экспорт
	ФИОВведено = НЕ ПустаяСтрока(Форма.ФИОФизическихЛиц.Фамилия)
						ИЛИ НЕ ПустаяСтрока(Форма.ФИОФизическихЛиц.Имя)
						ИЛИ НЕ ПустаяСтрока(Форма.ФИОФизическихЛиц.Отчество);
	Если ФИОВведено Тогда
		Форма.Элементы.ГруппаСтраницыПолноеИмя.ТекущаяСтраница = Форма.Элементы.ГруппаСтраницаПолноеИмя;
	Иначе
		Форма.Элементы.ГруппаСтраницыПолноеИмя.ТекущаяСтраница = Форма.Элементы.ГруппаСтраницаПолноеИмяСкрытое;
	КонецЕсли;
КонецПроцедуры

Процедура УстановитьРасширеннуюПодсказкуЭлементуФормы(Форма, ИмяЭлемента, ТекстПодсказки) Экспорт
	
	ЭлементФормы = Форма.Элементы.Найти(ИмяЭлемента);
	Если ЭлементФормы <> Неопределено Тогда
		ЭлементФормы.РасширеннаяПодсказка.Заголовок = ТекстПодсказки;
	КонецЕсли;
	
КонецПроцедуры

Функция УдостоверениеЛичностиПоУмолчанию(ИнформацияОбУдостоверенииЛичности) Экспорт
	
	Возврат (НЕ ЗначениеЗаполнено(ИнформацияОбУдостоверенииЛичности.ВидДокумента))
		И (НЕ ЗначениеЗаполнено(ИнформацияОбУдостоверенииЛичности.Серия))
		И (НЕ ЗначениеЗаполнено(ИнформацияОбУдостоверенииЛичности.Номер))
		И (НЕ ЗначениеЗаполнено(ИнформацияОбУдостоверенииЛичности.ДатаВыдачи))
		И (НЕ ЗначениеЗаполнено(ИнформацияОбУдостоверенииЛичности.СрокДействия))
		И (НЕ ЗначениеЗаполнено(ИнформацияОбУдостоверенииЛичности.КемВыдан))
		И (НЕ ЗначениеЗаполнено(ИнформацияОбУдостоверенииЛичности.КодПодразделения));
		
	КонецФункции
	
Процедура ОбновитьДоступностьПолейВводаГражданства(Форма, ДатаСеанса) Экспорт
	
	Форма.Элементы.ГражданствоФизическихЛицСтрана.Доступность = (Форма.ГражданствоФизическихЛицЛицоБезГражданства = 0);
	
	ОбновитьПолеГражданствоПериод(Форма, ДатаСеанса);
	
КонецПроцедуры
	