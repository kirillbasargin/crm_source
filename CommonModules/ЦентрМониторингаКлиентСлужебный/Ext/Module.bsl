#Область СлужебныйПрограммныйИнтерфейс

Процедура ПриВыполненииСтандартныхПериодическихПроверокНаКлиенте(Параметры) Экспорт
    
    Окна = ПолучитьОкна();
    АктивныхОкон = 0;
	Если Окна <> Неопределено Тогда
		Для Каждого ТекОкно Из Окна Цикл
	        Если Не ТекОкно.Основное Тогда
	            АктивныхОкон = АктивныхОкон + 1;
	        КонецЕсли;
		КонецЦикла;
	КонецЕсли;
    
    ПараметрыПриложенияЦентрМониторинга = ПолучитьПараметрыПриложения();
    ПараметрыПриложенияЦентрМониторинга["ИнформацияКлиента"].Вставить("АктивныхОкон", АктивныхОкон);
    
    Параметры.Вставить("ЦентрМониторинга", Новый ФиксированноеСоответствие(ПараметрыПриложенияЦентрМониторинга));
    
    Замеры = Новый Соответствие;
    Замеры.Вставить(0, Новый Массив);
    Замеры.Вставить(1, Новый Соответствие);
    Замеры.Вставить(2, Новый Соответствие);
    ПараметрыПриложенияЦентрМониторинга.Вставить("Замеры", Замеры);
    
КонецПроцедуры

Процедура ПослеВыполненияСтандартныхПериодическихПроверокНаКлиенте(Параметры) Экспорт
    
    ПараметрыПриложенияЦентрМониторинга = ПолучитьПараметрыПриложения();
    ПараметрыПриложенияЦентрМониторинга.Вставить("РегистрироватьБизнесСтатистику", Параметры.ЦентрМониторинга["РегистрироватьБизнесСтатистику"]);
        
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// См. описание этой же процедуры в общем модуле
// ОбщегоНазначенияКлиентПереопределяемый.
//
Процедура ПриНачалеРаботыСистемы(Параметры) Экспорт
    
    ЦентрМониторингаПараметрыПриложения = ПолучитьПараметрыПриложения();
	    
КонецПроцедуры

Функция ПолучитьПараметрыПриложения() Экспорт
    
    Если ПараметрыПриложения = Неопределено Тогда
    	ПараметрыПриложения = Новый Соответствие;
    КонецЕсли;
    
    ИмяПараметра = "СтандартныеПодсистемы.ЦентрМониторинга";
    Если ПараметрыПриложения[ИмяПараметра] = Неопределено Тогда
        
        ПараметрыКлиента = ПараметрыКлиента();
        
        ПараметрыПриложения.Вставить(ИмяПараметра, Новый Соответствие);
        ЦентрМониторингаПараметрыПриложения = ПараметрыПриложения[ИмяПараметра];
        ЦентрМониторингаПараметрыПриложения.Вставить("РегистрироватьБизнесСтатистику", ПараметрыКлиента["РегистрироватьБизнесСтатистику"]);
        
        Замеры = Новый Соответствие;
        Замеры.Вставить(0, Новый Массив);
        Замеры.Вставить(1, Новый Соответствие);
        Замеры.Вставить(2, Новый Соответствие);
        ЦентрМониторингаПараметрыПриложения.Вставить("Замеры", Замеры);
        
        ЦентрМониторингаПараметрыПриложения.Вставить("ИнформацияКлиента", ПолучитьИнформациюКлиента());
        
    Иначе
        
        ЦентрМониторингаПараметрыПриложения = ПараметрыПриложения["СтандартныеПодсистемы.ЦентрМониторинга"];
                       
    КонецЕсли;
    
    Возврат ЦентрМониторингаПараметрыПриложения; 
    
КонецФункции

Функция ПолучитьИнформациюКлиента()
    
    ИнформацияКлиента = Новый Соответствие;
    
    ИнформацияЭкраны = Новый Массив;
    ЭкраныКлиента = ПолучитьИнформациюЭкрановКлиента();
    Для Каждого ТекЭкран Из ЭкраныКлиента Цикл
        ИнформацияЭкраны.Добавить(РазрешениеЭкранаВСтроку(ТекЭкран));
    КонецЦикла;
    
    ИнформацияКлиента.Вставить("ЭкраныКлиента", ИнформацияЭкраны);
    ИнформацияКлиента.Вставить("ПараметрыКлиента", ПараметрыКлиента());
    ИнформацияКлиента.Вставить("ИнформацияОСистеме", ПолучитьИнформациюОСистеме());
    ИнформацияКлиента.Вставить("АктивныхОкон", 0);
    
    Возврат ИнформацияКлиента;
    
КонецФункции

Функция РазрешениеЭкранаВСтроку(Экран)
    
    Возврат Формат(Экран.Ширина, "ЧГ=0") + "x" + Формат(Экран.Высота, "ЧГ=0");
    
КонецФункции

Функция ПараметрыКлиента()
    
    ПараметрыКлиента = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтандартныеПодсистемыКлиент.ПараметрыРаботыКлиентаПриЗапуске(),"ЦентрМониторинга");
    Если ПараметрыКлиента = Неопределено Тогда
        ПараметрыКлиента = Новый Структура("ЧасовойПоясСеанса, ХешПользователя, РегистрироватьБизнесСтатистику", Неопределено, Неопределено, Ложь);
    КонецЕсли;
    
    ПараметрыКлиентаИнформация = Новый Соответствие;
    Для Каждого ТекПараметр Из ПараметрыКлиента Цикл
        ПараметрыКлиентаИнформация.Вставить(ТекПараметр.Ключ, ТекПараметр.Значение);
    КонецЦикла;
        
    Возврат ПараметрыКлиентаИнформация; 
        
КонецФункции

Функция ПолучитьИнформациюОСистеме()
    
    СисИнфо = Новый СистемнаяИнформация();
    
    СисИнфоИнформация = Новый Соответствие;
    СисИнфоИнформация.Вставить("ВерсияОС", СтрЗаменить(СисИнфо.ВерсияОС, ".", "☺"));
    СисИнфоИнформация.Вставить("ОперативнаяПамять", Формат((ЦЕЛ(СисИнфо.ОперативнаяПамять/512) + 1) * 512, "ЧГ=0"));
    СисИнфоИнформация.Вставить("Процессор", СтрЗаменить(СисИнфо.Процессор, ".", "☺"));
    СисИнфоИнформация.Вставить("ТипПлатформы", СтрЗаменить(Строка(СисИнфо.ТипПлатформы), ".", "☺"));
        
    ИнформацияПрограммыПросмотра = "";
    #Если ТолстыйКлиентУправляемоеПриложение Тогда
        ИнформацияПрограммыПросмотра = "ТолстыйКлиентУправляемоеПриложение";
    #ИначеЕсли ТолстыйКлиентОбычноеПриложение Тогда
        ИнформацияПрограммыПросмотра = "ТолстыйКлиент";
    #ИначеЕсли ТонкийКлиент Тогда
        ИнформацияПрограммыПросмотра = "ТонкийКлиент";
    #ИначеЕсли ВебКлиент Тогда                                                          
        ИнформацияПрограммыПросмотра = "ВебКлиент";
    #КонецЕсли
    
    СисИнфоИнформация.Вставить("ИнформацияПрограммыПросмотра", ИнформацияПрограммыПросмотра);
    
    Возврат СисИнфоИнформация; 
    
КонецФункции

#КонецОбласти