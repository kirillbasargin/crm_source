#Область СлужебныеПроцедурыИФункции

Функция ПриОпределенииНастроек() Экспорт
	
	Настройки = Новый Структура("ПредметыШаблонов, ОбщиеРеквизиты");
	Настройки.Вставить("ИспользоватьПроизвольныеПараметры", Ложь);
	Настройки.Вставить("ЗначенияПараметровСКД", Новый Структура);
	
	ОбщиеРеквизитыДерево = ШаблоныСообщенийСлужебный.ОпределитьОбщиеРеквизиты();
	Настройки.ОбщиеРеквизиты = ШаблоныСообщенийСлужебный.ОбщиеРеквизиты(ОбщиеРеквизитыДерево);
	Настройки.ПредметыШаблонов = ОпределитьПредметыШаблонов();
	
	ШаблоныСообщенийПереопределяемый.ПриОпределенииНастроек(Настройки);
	Настройки.ОбщиеРеквизиты = ОбщиеРеквизитыДерево;
	
	Для каждого ПредметШаблона Из Настройки.ПредметыШаблонов Цикл
		Для каждого ПараметрСКД Из Настройки.ЗначенияПараметровСКД Цикл
			Если НЕ ПредметШаблона.ЗначенияПараметровСКД.Свойство(ПараметрСКД.Ключ)
				ИЛИ ПредметШаблона.ЗначенияПараметровСКД[ПараметрСКД.Ключ] = Null Тогда
					ПредметШаблона.ЗначенияПараметровСКД.Вставить(ПараметрСКД.Ключ, Настройки.ЗначенияПараметровСКД[ПараметрСКД.Ключ]);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Настройки.ПредметыШаблонов.Сортировать("Представление");
	
	Возврат Настройки;
	
КонецФункции

Функция ОпределитьПредметыШаблонов()
	ИмяМакетаШаблонаПоУмолчанию = "ДанныеШаблонаСообщений";
	
	ОснованияДляШаблоновСообщений = Новый ТаблицаЗначений;
	ОснованияДляШаблоновСообщений.Колонки.Добавить("Имя", Новый ОписаниеТипов("Строка"));
	ОснованияДляШаблоновСообщений.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	ОснованияДляШаблоновСообщений.Колонки.Добавить("Макет", Новый ОписаниеТипов("Строка"));
	ОснованияДляШаблоновСообщений.Колонки.Добавить("ЗначенияПараметровСКД", Новый ОписаниеТипов("Структура"));
	
	ТипыПредметовШаблоновСообщений = Метаданные.ОпределяемыеТипы.ПредметШаблонаСообщения.Тип.Типы();
	Для каждого ТипПредметаШаблонаСообщения Из ТипыПредметовШаблоновСообщений Цикл
		Если ТипПредметаШаблонаСообщения <> Тип("СправочникСсылка.ИдентификаторыОбъектовМетаданных") Тогда
			Назначение = ОснованияДляШаблоновСообщений.Добавить();
			МетаданныеОбъект = Метаданные.НайтиПоТипу(ТипПредметаШаблонаСообщения);
			Назначение.Имя = МетаданныеОбъект.ПолноеИмя();
			Назначение.Представление = МетаданныеОбъект.Представление();
			Если МетаданныеОбъект.Макеты.Найти(ИмяМакетаШаблонаПоУмолчанию) <> Неопределено Тогда
				Назначение.Макет = ИмяМакетаШаблонаПоУмолчанию;
				
				// Параметры СКД
				МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(Назначение.Имя);
				МакетСКД = МенеджерОбъекта.ПолучитьМакет(ИмяМакетаШаблонаПоУмолчанию);
				АдресСхемы = ПоместитьВоВременноеХранилище(МакетСКД);
				КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных;
				КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемы));
				Для Каждого ДоступныйПараметр Из КомпоновщикНастроек.Настройки.ПараметрыДанных.ДоступныеПараметры.Элементы Цикл
					ИмяПараметра = Строка(ДоступныйПараметр.Параметр);
					Если НЕ (СтрСравнить(ИмяПараметра , "Период") = 0 
								ИЛИ СтрСравнить(ИмяПараметра, МетаданныеОбъект.Имя) = 0) Тогда
									Назначение.ЗначенияПараметровСКД.Вставить(ИмяПараметра, NULL);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ОснованияДляШаблоновСообщений;
	
КонецФункции

#КонецОбласти
