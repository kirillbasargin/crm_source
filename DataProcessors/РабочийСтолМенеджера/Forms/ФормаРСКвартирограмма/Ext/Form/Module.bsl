
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СписокОбъектовСтроительства = ПолучитьСписокОбъектовСтроительства(ПользователиКлиентСервер.ТекущийПользователь());
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ОбъектСтроительства", СписокОбъектовСтроительства, ВидСравненияКомпоновкиДанных.ВСписке, , Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Период", ТекущаяДата(), Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ОбъектСтроительства", Неопределено, Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "КоличествоКомнат", Неопределено, Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "СтатусОН", Неопределено, Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Собственник", Неопределено, Истина);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСписокОбъектовСтроительства(ТекущийПользователь)
	
	СписокРезультат = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
				|	ГруппыПользователейСостав.Ссылка КАК Группа
				|ПОМЕСТИТЬ ВТ_СписокГрупп
				|ИЗ
				|	Справочник.ГруппыПользователей.Состав КАК ГруппыПользователейСостав
				|ГДЕ
				|	ГруппыПользователейСостав.Пользователь = &Пользователь
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	СоответствиеГруппПользователейОфисам.Проект КАК Проект
				|ПОМЕСТИТЬ ВТ_Проекты
				|ИЗ
				|	РегистрСведений.СоответствиеГруппПользователейОфисам КАК СоответствиеГруппПользователейОфисам
				|ГДЕ
				|	СоответствиеГруппПользователейОфисам.ГруппаПользователей В
				|			(ВЫБРАТЬ
				|				ВТ_СписокГрупп.Группа КАК Группа
				|			ИЗ
				|				ВТ_СписокГрупп КАК ВТ_СписокГрупп)
				|
				|СГРУППИРОВАТЬ ПО
				|	СоответствиеГруппПользователейОфисам.Проект
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	ОбъектыСтроительства.Ссылка КАК Ссылка
				|ИЗ
				|	Справочник.ОбъектыСтроительства КАК ОбъектыСтроительства
				|ГДЕ
				|	ОбъектыСтроительства.Проект В
				|			(ВЫБРАТЬ
				|				ВТ_Проекты.Проект КАК Проект
				|			ИЗ
				|				ВТ_Проекты КАК ВТ_Проекты)
				|	И НЕ ОбъектыСтроительства.ЭтоГруппа";
	
	Запрос.УстановитьПараметр("Пользователь", ТекущийПользователь);
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			СписокРезультат.Добавить(Выборка.Ссылка);
		КонецЦикла;
	КонецЕсли;
	
	Возврат СписокРезультат;
	
КонецФункции

&НаКлиенте
Процедура ОбъектСтроительства1ПриИзменении(Элемент)
	ОбновитьПоле("ОбъектСтроительства", ОбъектСтроительства);
КонецПроцедуры

&НаКлиенте
Процедура КоличествоКомнат1ПриИзменении(Элемент)
	ОбновитьПоле("КоличествоКомнат", КоличествоКомнат);
КонецПроцедуры

&НаКлиенте
Процедура СтатусОН1ПриИзменении(Элемент)
	ОбновитьПоле("СтатусОН", СтатусОН);
КонецПроцедуры

&НаСервере
Процедура ОбновитьПоле(Поле, Значение)
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, Поле, Значение, Истина);
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьЗначение(Элемент.ТекущиеДанные.ОбъектНедвижимости);
	
КонецПроцедуры

&НаКлиенте
Процедура СобственникПриИзменении(Элемент)
	ОбновитьПоле("Собственник", Собственник);
КонецПроцедуры

&НаКлиенте
Процедура ОбъектСтроительства1Очистка(Элемент, СтандартнаяОбработка)
	ОбновитьПоле("ОбъектСтроительства", Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура СобственникОчистка(Элемент, СтандартнаяОбработка)
	ОбновитьПоле("Собственник", Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура КоличествоКомнат1Очистка(Элемент, СтандартнаяОбработка)
	ОбновитьПоле("КоличествоКомнат", Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура СтатусОН1Очистка(Элемент, СтандартнаяОбработка)
	ОбновитьПоле("СтатусОН", Неопределено);
КонецПроцедуры
