
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)	
	ОбновитьНадписиГиперСсылок(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("ОбновитьКоличествоДокументовВЗаголовкахГиперСсылок", 300);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура АктуальныеВзаимодействия(Команда)
	
	ПараметрыОткрываемойФормы = Новый Структура;
	
	ТекущийПользователь = ПользователиКлиентСервер.ТекущийПользователь();
	Если ОтборПоОтветственному Тогда
		Ответственный = ТекущийПользователь;
	Иначе
		Ответственный = ПолучитьОтборПоОтветственному(ТекущийПользователь);
		ПараметрыОткрываемойФормы.Вставить("Заголовок", "Взаимодействия (по группе сотрудников)");
	КонецЕсли;
	
	ПараметрыОткрываемойФормы.Вставить("ОтборДляСтаршегоМенеджера", Истина);
	ПараметрыОткрываемойФормы.Вставить("Ответственный", Ответственный);
	ПараметрыОткрываемойФормы.Вставить("Отбор", Неопределено);
	ПараметрыОткрываемойФормы.Вставить("Отменено", Ложь);
	ПараметрыОткрываемойФормы.Вставить("Рассмотрено", Ложь);
	
	ТекущаяДата = ТекущаяДата();
	ПараметрыОткрываемойФормы.Вставить("ДатаОт", НачалоДня(ТекущаяДата));
	ПараметрыОткрываемойФормы.Вставить("ДатаДо", КонецДня(ТекущаяДата));
	
	ПараметрыОткрываемойФормы.Вставить("Проект", ПолучитьСписокПроектов(ТекущийПользователь));
	
	ПараметрыОткрываемойФормы.Вставить("Запланировано", Истина);
	
	ФормаСпискаПараметрическая = ПолучитьФорму("ЖурналДокументов.Взаимодействия.Форма.ФормаСпискаПараметрическая", ПараметрыОткрываемойФормы, ЭтотОбъект);
	Если ФормаСпискаПараметрическая.Открыта() Тогда
		ФормаСпискаПараметрическая.Закрыть();
	КонецЕсли;
	
	ОткрытьФорму("ЖурналДокументов.Взаимодействия.Форма.ФормаСпискаПараметрическая", ПараметрыОткрываемойФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершенныеВзаимодействия(Команда)
	
	ПараметрыОткрываемойФормы = Новый Структура;
	
	ТекущийПользователь = ПользователиКлиентСервер.ТекущийПользователь();
	
	Если ОтборПоОтветственному Тогда
		Ответственный = ТекущийПользователь;
	Иначе
		Ответственный = ПолучитьОтборПоОтветственному(ТекущийПользователь);
		ПараметрыОткрываемойФормы.Вставить("Заголовок", "Взаимодействия (по группе сотрудников)");
	КонецЕсли;
	
	ПараметрыОткрываемойФормы.Вставить("ОтборДляСтаршегоМенеджера", Истина);
	ПараметрыОткрываемойФормы.Вставить("Ответственный", Ответственный);
	ПараметрыОткрываемойФормы.Вставить("Отбор", Неопределено);
	
	ТекущаяДата = ТекущаяДата();
	ПараметрыОткрываемойФормы.Вставить("ДатаОт", НачалоДня(ТекущаяДата));
	ПараметрыОткрываемойФормы.Вставить("ДатаДо", КонецДня(ТекущаяДата));
	
	ПараметрыОткрываемойФормы.Вставить("Завершено", Истина);
	ПараметрыОткрываемойФормы.Вставить("Отменено", Ложь);
	ПараметрыОткрываемойФормы.Вставить("Рассмотрено", Истина);
	ПараметрыОткрываемойФормы.Вставить("Проект", ПолучитьСписокПроектов(ТекущийПользователь));
	
	ФормаСпискаПараметрическая = ПолучитьФорму("ЖурналДокументов.Взаимодействия.Форма.ФормаСпискаПараметрическая", ПараметрыОткрываемойФормы, ЭтотОбъект);
	Если ФормаСпискаПараметрическая.Открыта() Тогда
		ФормаСпискаПараметрическая.Закрыть();
	КонецЕсли;
	
	ОткрытьФорму("ЖурналДокументов.Взаимодействия.Форма.ФормаСпискаПараметрическая", ПараметрыОткрываемойФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПросроченныеВзаимодействия(Команда)
	
	ПараметрыОткрываемойФормы = Новый Структура;
	
	ТекущийПользователь = ПользователиКлиентСервер.ТекущийПользователь();
	
	Если ОтборПоОтветственному Тогда
		Ответственный = ТекущийПользователь;
	Иначе
		Ответственный = ПолучитьОтборПоОтветственному(ТекущийПользователь);
		ПараметрыОткрываемойФормы.Вставить("Заголовок", "Взаимодействия (по группе сотрудников)");
	КонецЕсли;
	
	ПараметрыОткрываемойФормы.Вставить("ОтборДляСтаршегоМенеджера", Истина);
	ПараметрыОткрываемойФормы.Вставить("Ответственный", Ответственный);
	ПараметрыОткрываемойФормы.Вставить("Отбор", Неопределено);
	ПараметрыОткрываемойФормы.Вставить("ДатаОт", НачалоДня(ТекущаяДата()));
	
	ПараметрыОткрываемойФормы.Вставить("Проект", ПолучитьСписокПроектов(ТекущийПользователь));
	ПараметрыОткрываемойФормы.Вставить("Просроченные", Истина);
	
	ФормаСпискаПараметрическая = ПолучитьФорму("ЖурналДокументов.Взаимодействия.Форма.ФормаСпискаПараметрическая", ПараметрыОткрываемойФормы, ЭтотОбъект);
	Если ФормаСпискаПараметрическая.Открыта() Тогда
		ФормаСпискаПараметрическая.Закрыть();
	КонецЕсли;
		
	ОткрытьФорму("ЖурналДокументов.Взаимодействия.Форма.ФормаСпискаПараметрическая", ПараметрыОткрываемойФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаявкиОтГПТ(Команда)   
	
	МассивСтатусов = Новый Массив;
	МассивСтатусов.Добавить(ПредопределенноеЗначение("Перечисление.СтатусЗаявкиCallBack.НеОбработано"));
	МассивСтатусов.Добавить(ПредопределенноеЗначение("Перечисление.СтатусЗаявкиCallBack.БылиПопыткиДозвониться"));
	
	ТекущийПользователь = ПользователиКлиентСервер.ТекущийПользователь();
	
	Если ОтборПоОтветственному Тогда 
		Ответственный = ТекущийПользователь; 
	Иначе
		Ответственный = ПолучитьОтборПоОтветственному(ТекущийПользователь);
	КонецЕсли;
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ответственный", Ответственный);
	ПараметрыФормы.Вставить("Статус", МассивСтатусов);
	ПараметрыФормы.Вставить("Проведен", Истина);
	ПараметрыФормы.Вставить("Проект", ПолучитьСписокПроектов(ТекущийПользователь));
	
	ПараметрыОткрываемойФормы = Новый Структура("Отбор", ПараметрыФормы);
	//Новый Структура("Ответственный,Статус,Проведен", ПользователиКлиентСервер.ТекущийПользователь(),МассивСтатусов,Истина));
	
	ОткрытьФорму("Документ.ЗаявкаНаЗвонок.ФормаСписка", ПараметрыОткрываемойФормы, ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ЗаявкиНаСделкуНаСогласовании(Команда)
	
	ПараметрыОткрываемойФормы = Новый Структура(); 	
	ТекущийПользователь = ПользователиКлиентСервер.ТекущийПользователь();
	
	Если ОтборПоОтветственному Тогда 
		Ответственный = ТекущийПользователь; 
	Иначе
		Ответственный = ПолучитьОтборПоОтветственному(ТекущийПользователь);
	КонецЕсли;
	ПараметрыОткрываемойФормы.Вставить("Автор", Ответственный);
	
	ПараметрыОткрываемойФормы.Вставить("ОтборДляСтаршегоМенеджера", Истина);
	
	ТекущаяДата = ТекущаяДата();
	ПараметрыОткрываемойФормы.Вставить("ДатаОт", НачалоДня(ТекущаяДата));
	ПараметрыОткрываемойФормы.Вставить("ДатаДо", КонецДня(ТекущаяДата));
	
	ПараметрыОткрываемойФормы.Вставить("Проведен", Истина);
	ПараметрыОткрываемойФормы.Вставить("Проект", ПолучитьСписокПроектов(ТекущийПользователь));
	ПараметрыОткрываемойФормы.Вставить("Статус", ПолучитьСписокСтатусовСделки());
	
	ФормаСписка = ПолучитьФорму("Документ.ЗаявкаНаСделку.ФормаСписка", ПараметрыОткрываемойФормы, ЭтотОбъект);
	Если ФормаСписка.Открыта() Тогда
		ФормаСписка.Закрыть();
	КонецЕсли;
	
	ОткрытьФорму("Документ.ЗаявкаНаСделку.ФормаСписка", ПараметрыОткрываемойФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСписокСтатусовСделки()
	
	Статусы = Новый СписокЗначений;
	Статусы.Добавить(Перечисления.СтатусыСделки.Согласована);
	Статусы.Добавить(Перечисления.СтатусыСделки.СогласованиеЮриста);
	Статусы.Добавить(Перечисления.СтатусыСделки.СогласованиеИБ);
	Статусы.Добавить(Перечисления.СтатусыСделки.СогласованиеУПН);
	
	Возврат Статусы;
	
КонецФункции

&НаКлиенте
Процедура Шахматка(Команда)
	
	ОткрытьФорму("Отчет.Шахматка.Форма.ФормаОтчета", , ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетПоВзаимодействиям(Команда)
	ОткрытьФорму("Отчет.ОтчетПоВзаимодействиям.Форма", , ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура РабочийСтолКлиента(Команда)
	ОткрытьФорму("Обработка.РаботаСКлиентом.Форма.Форма");
КонецПроцедуры

&НаКлиенте
Процедура ОтчетМенеджера(Команда)
	
	ПараметрыОткрываемойФормы = Новый Структура("Отбор,СформироватьПриОткрытии", 
													Новый Структура("Стадия", ПредопределенноеЗначение("Перечисление.СтадииЗапроса.Переговоры")),
													Истина);
	ОткрытьФорму("Отчет.ОтчетМенеджера.ФормаОбъекта", ПараметрыОткрываемойФормы, ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ОтчетПоИсточникамРекламы(Команда)
	
	ОткрытьФорму("Отчет.ОтчетПоИсточникамРекламы.ФормаОбъекта",, ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ЭффективностьМенеджера(Команда)
	
	ПараметрыОткрываемойФормы = Новый Структура("СформироватьПриОткрытии", Истина);
	ОткрытьФорму("Отчет.ЭффективностьМенеджера.ФормаОбъекта", ПараметрыОткрываемойФормы, ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ОтборПоОтветственномуПриИзменении(Элемент)
	
	ОбновитьКоличествоДокументовВЗаголовкахГиперСсылок();
	
КонецПроцедуры

&НаКлиенте
Процедура ОчередиБронирования(Команда)
	
	ТекущийПользователь = ПользователиКлиентСервер.ТекущийПользователь();
	
	Если ОтборПоОтветственному Тогда 
		Ответственный = ТекущийПользователь; 
	Иначе
		Ответственный = ПолучитьОтборПоОтветственному(ТекущийПользователь, Истина);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ОтборДляСтаршегоМенеджера", Истина);
	ПараметрыФормы.Вставить("Ответственный", Ответственный);
	ПараметрыФормы.Вставить("НачалоДня", НачалоДня(ТекущаяДата()));
	ПараметрыФормы.Вставить("Проект", ПолучитьСписокПроектов(ТекущийПользователь, Истина));
	
	ФормаСписка = ПолучитьФорму("РегистрСведений.СрокиБронированияОбъектовНедвижимости.Форма.СрокиИОчередьБронированияПоОтветственному", ПараметрыФормы, ЭтотОбъект, ЭтаФорма);
	Если ФормаСписка.Открыта() Тогда
		//ФормаСписка.Закрыть();
		ФормаСписка.КонтекстВыбора = ПараметрыФормы;
	КонецЕсли;
	ФормаСписка.Открыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетПоВыданнымПодаркам(Команда)
	ОткрытьФорму("Отчет.ОтчетПоВыданнымПодаркам.Форма", , ЭтотОбъект);
КонецПроцедуры                                       

&НаКлиенте
Процедура ПодборКвартир(Команда)
	ОткрытьФорму("Отчет.Квартирограмма.Форма.ФормаПодбора", , ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПодборЗУ(Команда)	
	ОткрытьФорму("Отчет.Квартирограмма.Форма.ФормаПодбора", Новый Структура("Отбор, КлючТекущегоВарианта", Новый Структура("ПроектВидНедвижимости", ПредопределенноеЗначение("Перечисление.ВидыОбъектовНедвижимости.ЗемельныйУчасток")), ОпределитьВариантОтчета()), ЭтотОбъект);	
КонецПроцедуры	

&НаКлиенте
Процедура ОтчетПоВзаимодействиямЗУ(Команда)
	ОткрытьФорму("Отчет.ОтчетПоВзаимодействиямЗУ.Форма.ФормаОтчета", , ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОпределитьВариантОтчета()
	
	КлючТекущегоВарианта = "ЗУ";
	
	Запрос = Новый Запрос;
	Запрос.Текст =
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
				|	ВариантыОтчетов.Ссылка КАК ВариантСсылка,
				|	ВариантыОтчетов.КлючВарианта КАК КлючВарианта
				|ИЗ
				|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
				|ГДЕ
				|	ВариантыОтчетов.Отчет.Имя = ""Квартирограмма""
				|	И ВариантыОтчетов.Автор = &Автор
				|	И ВариантыОтчетов.Пользовательский
				|	И ВариантыОтчетов.Наименование ПОДОБНО &КлючВарианта
				|	И НЕ ВариантыОтчетов.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("КлючВарианта", КлючТекущегоВарианта + "%");
	Запрос.УстановитьПараметр("Автор", ПользователиКлиентСервер.АвторизованныйПользователь());
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		КлючТекущегоВарианта = Выборка.КлючВарианта;	
	КонецЕсли;
	
	Возврат КлючТекущегоВарианта;	
		
КонецФункции

&НаКлиенте
Процедура ОбновитьКоличествоДокументовВЗаголовкахГиперСсылок()

	ОбновитьНадписиГиперСсылок(ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСтруктураНаСервере(Ответственные)

	Результат = Новый Структура("ЗапланированныеВзаимодействия, ЗавершенныеВзаимодействия, ПросроченныеВзаимодействия, ЗаявкиОтГПТ, ЗаявкиНаСделку, ОчередиБронирования", 0, 0, 0, 0, 0, 0);	

	Запрос = Новый Запрос;
	Запрос.Текст = 
				"ВЫБРАТЬ
				|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Взаимодействия.Ссылка) КАК КоличествоДокументов
				|ИЗ
				|	ЖурналДокументов.Взаимодействия КАК Взаимодействия
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПредметыПапкиВзаимодействий КАК ПредметыПапкиВзаимодействий
				|		ПО Взаимодействия.Ссылка = ПредметыПапкиВзаимодействий.Взаимодействие
				|ГДЕ
				|	Взаимодействия.Ответственный В(&Ответственные)
				|	И ЕСТЬNULL(ПредметыПапкиВзаимодействий.РассмотретьПосле, ДАТАВРЕМЯ(1, 1, 1)) МЕЖДУ &НачалоДня И &КонецДня
				|	И НЕ ЕСТЬNULL(ПредметыПапкиВзаимодействий.Рассмотрено, ЛОЖЬ)
				|	И НЕ Взаимодействия.Отменено
				|	И ВЫБОР
				|			КОГДА ПредметыПапкиВзаимодействий.Предмет ССЫЛКА Документ.Запрос
				|				ТОГДА ПредметыПапкиВзаимодействий.Предмет.Проект В (&СписокПроектов)
				|			ИНАЧЕ ЛОЖЬ
				|		КОНЕЦ
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Взаимодействия.Ссылка) КАК КоличествоДокументов
				|ИЗ
				|	ЖурналДокументов.Взаимодействия КАК Взаимодействия
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПредметыПапкиВзаимодействий КАК ПредметыПапкиВзаимодействий
				|		ПО Взаимодействия.Ссылка = ПредметыПапкиВзаимодействий.Взаимодействие
				|ГДЕ
				|	Взаимодействия.Ответственный В(&Ответственные)
				|	И ВЫБОР
				|			КОГДА ЕСТЬNULL(ПредметыПапкиВзаимодействий.Рассмотрено, ЛОЖЬ)
				|					ИЛИ Взаимодействия.Отменено
				|				ТОГДА Взаимодействия.ДатаОкончания
				|			ИНАЧЕ ЕСТЬNULL(ПредметыПапкиВзаимодействий.РассмотретьПосле, ДАТАВРЕМЯ(1, 1, 1))
				|		КОНЕЦ <= &КонецДня
				|	И ЕСТЬNULL(ПредметыПапкиВзаимодействий.Рассмотрено, ЛОЖЬ)
				|	И НЕ Взаимодействия.Отменено
				|	И ВЫБОР
				|			КОГДА ЕСТЬNULL(ПредметыПапкиВзаимодействий.Рассмотрено, ЛОЖЬ) = ИСТИНА
				|					ИЛИ Взаимодействия.Отменено
				|				ТОГДА Взаимодействия.ДатаОкончания
				|			ИНАЧЕ ЕСТЬNULL(ПредметыПапкиВзаимодействий.РассмотретьПосле, ДАТАВРЕМЯ(1, 1, 1))
				|		КОНЕЦ >= &НачалоДня
				|	И ВЫБОР
				|			КОГДА ПредметыПапкиВзаимодействий.Предмет ССЫЛКА Документ.Запрос
				|				ТОГДА ПредметыПапкиВзаимодействий.Предмет.Проект В (&СписокПроектов)
				|			ИНАЧЕ ЛОЖЬ
				|		КОНЕЦ
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Взаимодействия.Ссылка) КАК КоличествоДокументов
				|ИЗ
				|	ЖурналДокументов.Взаимодействия КАК Взаимодействия
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПредметыПапкиВзаимодействий КАК ПредметыПапкиВзаимодействий
				|		ПО Взаимодействия.Ссылка = ПредметыПапкиВзаимодействий.Взаимодействие
				|ГДЕ
				|	Взаимодействия.Автор В(&Ответственные)
				|	И ЕСТЬNULL(ПредметыПапкиВзаимодействий.РассмотретьПосле, ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)) < &НачалоДня
				|	И НЕ ЕСТЬNULL(ПредметыПапкиВзаимодействий.Рассмотрено, ЛОЖЬ)
				|	И НЕ Взаимодействия.Отменено
				|	И ВЫБОР
				|			КОГДА ПредметыПапкиВзаимодействий.Предмет ССЫЛКА Документ.Запрос
				|				ТОГДА ПредметыПапкиВзаимодействий.Предмет.Проект В (&СписокПроектов)
				|			ИНАЧЕ ЛОЖЬ
				|		КОНЕЦ
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаявкаНаЗвонок.Ссылка) КАК КоличествоДокументов
				|ИЗ
				|	Документ.ЗаявкаНаЗвонок КАК ЗаявкаНаЗвонок
				|ГДЕ
				|	ЗаявкаНаЗвонок.Проведен
				|	И ЗаявкаНаЗвонок.Ответственный В(&Ответственные)
				|	И ЗаявкаНаЗвонок.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусЗаявкиCallBack.НеОбработано), ЗНАЧЕНИЕ(Перечисление.СтатусЗаявкиCallBack.БылиПопыткиДозвониться))
				|	И ЗаявкаНаЗвонок.Проект В(&СписокПроектов)
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	ЗаявкаНаСделку.Ссылка КАК Ссылка
				|ПОМЕСТИТЬ ВТ_СписокЗаявокНаСделкуПоМенеджеру
				|ИЗ
				|	Документ.ЗаявкаНаСделку КАК ЗаявкаНаСделку
				|ГДЕ
				|	ЗаявкаНаСделку.Проведен
				|	И ЗаявкаНаСделку.Дата МЕЖДУ &НачалоДня И &КонецДня
				|	И ЗаявкаНаСделку.Автор В(&Ответственные)
				|	И ЗаявкаНаСделку.Проект В(&СписокПроектов)
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СтатусыСделкиСрезПоследних.ЗаявкаНаСделку) КАК КоличествоДокументов
				|ИЗ
				|	РегистрСведений.СтатусыСделки.СрезПоследних(
				|			,
				|			ЗаявкаНаСделку В
				|				(ВЫБРАТЬ
				|					ВТ_СписокЗаявокНаСделкуПоМенеджеру.Ссылка КАК Ссылка
				|				ИЗ
				|					ВТ_СписокЗаявокНаСделкуПоМенеджеру КАК ВТ_СписокЗаявокНаСделкуПоМенеджеру)) КАК СтатусыСделкиСрезПоследних
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	МАКСИМУМ(ВложенныйЗапрос.ДатаКонцаСрокаБронирования) КАК ДатаКонцаСрокаБронирования,
				|	ВложенныйЗапрос.Регистратор КАК Регистратор,
				|	ВложенныйЗапрос.Период КАК Период,
				|	ВложенныйЗапрос.Ответственный КАК Ответственный,
				|	ВложенныйЗапрос.Клиент КАК Клиент,
				|	ВложенныйЗапрос.ДлительностьСрокаБронирования КАК ДлительностьСрокаБронирования,
				|	ВложенныйЗапрос.ОбъектНедвижимостиВладелец КАК ОбъектСтроительства,
				|	ВложенныйЗапрос.ОбъектНедвижимости КАК ОбъектНедвижимости
				|ПОМЕСТИТЬ ВТ_Брони
				|ИЗ
				|	(ВЫБРАТЬ
				|		СрокиБронированияОбъектовНедвижимостиСрезПоследних.ДлительностьСрокаБронирования КАК ДлительностьСрокаБронирования,
				|		СрокиБронированияОбъектовНедвижимостиСрезПоследних.ДатаКонцаСрокаБронирования КАК ДатаКонцаСрокаБронирования,
				|		СрокиБронированияОбъектовНедвижимостиСрезПоследних.Регистратор КАК Регистратор,
				|		СрокиБронированияОбъектовНедвижимостиСрезПоследних.Период КАК Период,
				|		СрокиБронированияОбъектовНедвижимостиСрезПоследних.Регистратор.Ответственный КАК Ответственный,
				|		СрокиБронированияОбъектовНедвижимостиСрезПоследних.Клиент КАК Клиент,
				|		СрокиБронированияОбъектовНедвижимостиСрезПоследних.ОбъектНедвижимости КАК ОбъектНедвижимости,
				|		СрокиБронированияОбъектовНедвижимостиСрезПоследних.ОбъектНедвижимости.Владелец КАК ОбъектНедвижимостиВладелец
				|	ИЗ
				|		РегистрСведений.СрокиБронированияОбъектовНедвижимости.СрезПоследних(
				|				,
				|				Регистратор.Ответственный В (&Ответственные)
				|					И ДатаКонцаСрокаБронирования >= &НачалоДня) КАК СрокиБронированияОбъектовНедвижимостиСрезПоследних
				|	ГДЕ
				|		СрокиБронированияОбъектовНедвижимостиСрезПоследних.ДокументЗапрос.Проект В(&СписокПроектов)
				|	
				|	ОБЪЕДИНИТЬ ВСЕ
				|	
				|	ВЫБРАТЬ
				|		ОчередьБронированияОстатки.ДокументБронирования.ДлительностьСрокаБронирования,
				|		ДАТАВРЕМЯ(1, 1, 1),
				|		ОчередьБронированияОстатки.ДокументБронирования,
				|		ОчередьБронированияОстатки.ДокументБронирования.Дата,
				|		ОчередьБронированияОстатки.ДокументБронирования.Ответственный,
				|		ОчередьБронированияОстатки.ДокументБронирования.Клиент,
				|		ОчередьБронированияОстатки.ОбъектНедвижимости,
				|		ОчередьБронированияОстатки.ОбъектНедвижимости.Владелец
				|	ИЗ
				|		РегистрНакопления.ОчередьБронирования.Остатки(, ДокументБронирования.Ответственный В (&Ответственные)) КАК ОчередьБронированияОстатки
				|	ГДЕ
				|		ОчередьБронированияОстатки.ДокументБронирования.Проект В(&СписокПроектов)) КАК ВложенныйЗапрос
				|
				|СГРУППИРОВАТЬ ПО
				|	ВложенныйЗапрос.Регистратор,
				|	ВложенныйЗапрос.Период,
				|	ВложенныйЗапрос.Ответственный,
				|	ВложенныйЗапрос.Клиент,
				|	ВложенныйЗапрос.ДлительностьСрокаБронирования,
				|	ВложенныйЗапрос.ОбъектНедвижимости,
				|	ВложенныйЗапрос.ОбъектНедвижимостиВладелец
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТ_Брони.Регистратор) КАК КоличествоДокументов
				|ИЗ
				|	ВТ_Брони КАК ВТ_Брони";
	
	//|ГДЕ
	//|	СтатусыСделкиСрезПоследних.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыСделки.СогласованиеЮриста), ЗНАЧЕНИЕ(Перечисление.СтатусыСделки.СогласованиеУПН))";
	
	//Если ОтборПоОтветственному Тогда
		//Запрос.УстановитьПараметр("Менеджер",	Менеджер);
	//Иначе
	//	Запрос.УстановитьПараметр("Менеджер",	ПолучитьОтборПоОтветственному(Менеджер));
	//	Запрос.Текст = СтрЗаменить(Запрос.Текст, "И ЗаявкаНаСделку.Автор В(&Менеджер)", "");
	//КонецЕсли;
	
	Запрос.УстановитьПараметр("НачалоДня",	НачалоДня(ТекущаяДата()));
	Запрос.УстановитьПараметр("КонецДня",	КонецДня(ТекущаяДата()));
	Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(ТекущаяДата()));
	Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(ТекущаяДата()));
	
	Запрос.УстановитьПараметр("Ответственные", Ответственные);
	Запрос.УстановитьПараметр("СписокПроектов", ПолучитьСписокПроектов(ПользователиКлиентСервер.ТекущийПользователь()));
		
	РезультатПакета = Запрос.ВыполнитьПакет();
	
	//Запланированные на текущий день взаимодействия
	ДобавитьЗначениеВРезультат(РезультатПакета, 0, Результат, "ЗапланированныеВзаимодействия");
	//Завершеные на текущий день взаимодействия
	ДобавитьЗначениеВРезультат(РезультатПакета, 1, Результат, "ЗавершенныеВзаимодействия");
	//Просроченные взаимодействия
	ДобавитьЗначениеВРезультат(РезультатПакета, 2, Результат, "ПросроченныеВзаимодействия");
	//Заявки ГПТ
	ДобавитьЗначениеВРезультат(РезультатПакета, 3, Результат, "ЗаявкиОтГПТ");
	//Заявки на сделку
	ДобавитьЗначениеВРезультат(РезультатПакета, 5, Результат, "ЗаявкиНаСделку");
	//ОчередиБронирования
	ДобавитьЗначениеВРезультат(РезультатПакета, 7, Результат, "ОчередиБронирования");	
	
	Возврат Результат;	
	
КонецФункции // ПолучитьСтруктураНаСервере()()

&НаСервереБезКонтекста
Процедура ДобавитьЗначениеВРезультат(РезультатПакета, НомерПакета, Результат, ИмяПараметра)
	
	Если НЕ РезультатПакета[НомерПакета].Пустой() Тогда
		Выборка = РезультатПакета[НомерПакета].Выбрать();
		Выборка.Следующий();
		Результат[ИмяПараметра] = Выборка.КоличествоДокументов;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьНадписиГиперСсылок(Форма)

	Если Форма.ОтборПоОтветственному Тогда 
		Ответственный = ПользователиКлиентСервер.ТекущийПользователь(); 
	Иначе
		Ответственный = ПолучитьОтборПоОтветственному(ПользователиКлиентСервер.ТекущийПользователь());
	КонецЕсли;
	
	КоличествоДокументов = ПолучитьСтруктураНаСервере(Ответственный);
	
	Элементы = Форма.Элементы;
	
	Элементы.АктуальныеВзаимодействия.Заголовок 	= "Запланировано на сегодня (" + КоличествоДокументов.ЗапланированныеВзаимодействия + ")";
	Элементы.ЗавершенныеВзаимодействия.Заголовок 	= "Завершено сегодня (" + КоличествоДокументов.ЗавершенныеВзаимодействия + ")";
	Элементы.ПросроченныйВзаимодействия.Заголовок 	= "Просроченные взаимодействия (" + КоличествоДокументов.ПросроченныеВзаимодействия + ")";	
	Элементы.ЗаявкиОтГПТ.Заголовок 					= "Заявки от ГПТ (" + КоличествоДокументов.ЗаявкиОтГПТ + ")";	
	Элементы.ЗаявкиНаСделкуНаСогласовании.Заголовок	= "Заявки на сделку на согласовании (" + КоличествоДокументов.ЗаявкиНаСделку + ")";
	Элементы.ОчередиБронирования.Заголовок			= "Очереди бронирования (" + КоличествоДокументов.ОчередиБронирования + ")";

КонецПроцедуры // ОбновитьНадписиГиперссылок()

&НаСервереБезКонтекста
Функция ПолучитьОтборПоОтветственному(Ответственный, ВМассив = Ложь)
	
	Если ВМассив Тогда
		СписокОтбора = Новый Массив;
	Иначе
		СписокОтбора = Новый СписокЗначений;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
				|	ГруппыПользователейСостав.Ссылка КАК Группа
				|ПОМЕСТИТЬ ВТ_СписокГрупп
				|ИЗ
				|	Справочник.ГруппыПользователей.Состав КАК ГруппыПользователейСостав
				|ГДЕ
				|	ГруппыПользователейСостав.Пользователь = &Пользователь
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ РАЗЛИЧНЫЕ
				|	ГруппыПользователейСостав.Пользователь КАК Пользователь
				|ИЗ
				|	Справочник.ГруппыПользователей.Состав КАК ГруппыПользователейСостав
				|ГДЕ
				|	ГруппыПользователейСостав.Ссылка В ИЕРАРХИИ
				|			(ВЫБРАТЬ
				|				ВТ.Группа
				|			ИЗ
				|				ВТ_СписокГрупп КАК ВТ)";
	
	Запрос.УстановитьПараметр("Пользователь", Ответственный);
	
	Результат = Запрос.Выполнить();	
	Если Результат.Пустой() Тогда
		СписокОтбора.Добавить(Ответственный);
	Иначе
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			СписокОтбора.Добавить(Выборка.Пользователь);
		КонецЦикла;
	КонецЕсли;
	
	Возврат СписокОтбора;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСписокПроектов(Пользователь, ВМассив = Ложь)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ГруппыПользователейСостав.Ссылка КАК Ссылка
	               |ПОМЕСТИТЬ ВТ_Группы
	               |ИЗ
	               |	Справочник.ГруппыПользователей.Состав КАК ГруппыПользователейСостав
	               |ГДЕ
	               |	ГруппыПользователейСостав.Пользователь = &Пользователь
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	СоответствиеГруппПользователейОфисам.Проект КАК Проект
	               |ИЗ
	               |	РегистрСведений.СоответствиеГруппПользователейОфисам КАК СоответствиеГруппПользователейОфисам
	               |ГДЕ
	               |	СоответствиеГруппПользователейОфисам.ГруппаПользователей В
	               |			(ВЫБРАТЬ
	               |				ВТ_Группы.Ссылка КАК Ссылка
	               |			ИЗ
	               |				ВТ_Группы КАК ВТ_Группы)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СоответствиеГруппПользователейОфисам.Проект";
	
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	Иначе
		Выборка = Результат.Выбрать();
		Если ВМассив Тогда
			Список = Новый Массив;
		Иначе
			Список = Новый СписокЗначений;
		КонецЕсли;
		
		Пока Выборка.Следующий() Цикл
			Список.Добавить(Выборка.Проект);
		КонецЦикла;
		
		Возврат Список;
	КонецЕсли;
	
КонецФункции

#КонецОбласти