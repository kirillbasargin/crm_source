
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбновитьНадписи(ЭтаФорма.Элементы, ПользователиКлиентСервер.ТекущийПользователь());
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьНадписи(Элементы, ТекущийПользователь)
	
	СтруктураПараметров = ПолучитьСтруктуруПараметров(ТекущийПользователь);
	
	Если СтруктураПараметров <> Неопределено Тогда
		Элементы.ЗаявкиНаСделку.Заголовок = "Заявки на сделку (" + СтруктураПараметров.ЧислоЗаявокНаСделку + ")";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаявкиНаСделку(Команда)
	
	ПараметрыФормы = Новый Структура;
	
	ПараметрыФормы.Вставить("Проведен", Истина);
	ПараметрыФормы.Вставить("Проект", ПолучитьСписокПроектов(ПользователиКлиентСервер.ТекущийПользователь()));
	
	ТекущаяДата = ТекущаяДата();
	ПараметрыФормы.Вставить("ДатаОт", НачалоДня(ТекущаяДата));
	ПараметрыФормы.Вставить("ДатаДо", КонецДня(ТекущаяДата));
	//ПараметрыФормы.Вставить("Статус", ПредопределенноеЗначение("Перечисление.СтатусыСделки.СогласованиеЮриста"));
	ПараметрыФормы.Вставить("ОтборДляЮристов", Истина);
	
	ФормаСписка = ПолучитьФорму("Документ.ЗаявкаНаСделку.ФормаСписка", ПараметрыФормы, ЭтотОбъект);
	Если ФормаСписка.Открыта() Тогда
		ФормаСписка.КонтекстВыбора = ПараметрыФормы;
	КонецЕсли;
	ФормаСписка.Открыть();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСтруктуруПараметров(Пользователь)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВЫРАЗИТЬ(ИсполнителиЗадач.ОсновнойОбъектАдресации КАК Справочник.Проекты) КАК Проект
	               |ПОМЕСТИТЬ ВТ_СписокПроектов
	               |ИЗ
	               |	РегистрСведений.ИсполнителиЗадач КАК ИсполнителиЗадач
	               |ГДЕ
	               |	ИсполнителиЗадач.Исполнитель = &Исполнитель
	               |	И ИсполнителиЗадач.РольИсполнителя = ЗНАЧЕНИЕ(Справочник.РолиИсполнителей.Юристы)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаявкаНаСделку.Ссылка) КАК КоличествоДокументов
	               |ПОМЕСТИТЬ ВТ_ПоЗаявкам
	               |ИЗ
	               |	Документ.ЗаявкаНаСделку КАК ЗаявкаНаСделку
	               |ГДЕ
	               |	ЗаявкаНаСделку.Дата МЕЖДУ &НачалоДня И &КонецДня
	               |	И ЗаявкаНаСделку.Проект В
	               |			(ВЫБРАТЬ
	               |				ВТ_СписокПроектов.Проект КАК Проект
	               |			ИЗ
	               |				ВТ_СписокПроектов КАК ВТ_СписокПроектов)
	               |	И ЗаявкаНаСделку.Проведен
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СтатусыСделкиСрезПоследних.ЗаявкаНаСделку) КАК КоличествоДокументов
	               |ИЗ
	               |	РегистрСведений.СтатусыСделки.СрезПоследних(
	               |			&КонецДня,
	               |			ЗаявкаНаСделку.Проект В
	               |				(ВЫБРАТЬ
	               |					ВТ_СписокПроектов.Проект КАК Проект
	               |				ИЗ
	               |					ВТ_СписокПроектов КАК ВТ_СписокПроектов)) КАК СтатусыСделкиСрезПоследних
	               |ГДЕ
	               |	СтатусыСделкиСрезПоследних.Период >= &НачалоДня
	               |	И СтатусыСделкиСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСделки.СогласованиеЮриста)";
	
	Запрос.УстановитьПараметр("Исполнитель", Пользователь);
	
	ТекущаяДата = ТекущаяДата();
	Запрос.УстановитьПараметр("НачалоДня", НачалоДня(ТекущаяДата));
	Запрос.УстановитьПараметр("КонецДня", КонецДня(ТекущаяДата));
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	Иначе
		СтруктураПараметров = Новый Структура;
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		
		СтруктураПараметров.Вставить("ЧислоЗаявокНаСделку", Выборка.КоличествоДокументов);
		
		Возврат СтруктураПараметров;
	КонецЕсли;
	
КонецФункции
	
&НаСервереБезКонтекста
Функция ПолучитьСписокПроектов(Пользователь)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВЫРАЗИТЬ(ИсполнителиЗадач.ОсновнойОбъектАдресации КАК Справочник.Проекты) КАК Проект
	               |ИЗ
	               |	РегистрСведений.ИсполнителиЗадач КАК ИсполнителиЗадач
	               |ГДЕ
	               |	ИсполнителиЗадач.Исполнитель = &Исполнитель
	               |	И ИсполнителиЗадач.РольИсполнителя = ЗНАЧЕНИЕ(Справочник.РолиИсполнителей.Юристы)";
	
	Запрос.УстановитьПараметр("Исполнитель", Пользователь);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	Иначе
		Выборка = Результат.Выбрать();
		Список = Новый СписокЗначений;
		
		Пока Выборка.Следующий() Цикл
			Список.Добавить(Выборка.Проект);
		КонецЦикла;
		
		Возврат Список;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("ОбновитьНадписиВЗаголовках", 300);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНадписиВЗаголовках()

	ОбновитьНадписи(ЭтаФорма.Элементы, ПользователиКлиентСервер.ТекущийПользователь());
	
КонецПроцедуры