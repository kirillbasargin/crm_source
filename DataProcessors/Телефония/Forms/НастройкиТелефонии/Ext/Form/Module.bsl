
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Отказ = Не УправлениеТелефониейСервер.ПолучитьИспользованиеТелефонии();
	Если Не Отказ Тогда
		ЗаполнитьНастройкиТелефонии();		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбработкаСтатусаТелефонии();	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменениеСтатусаТелефонии" Тогда 
		ОбработкаСтатусаТелефонии();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти   

#Область ОбработчикиСобытийЭлементовФормы

#КонецОбласти   

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПускКомпоненты(Команда)
	
	глТелефония["Зарегистрирована"] = УправлениеТелефониейКлиент.РегистрацияКомпонентыТелефонии();
	Если глТелефония["Зарегистрирована"] Тогда
		УправлениеТелефониейКлиент.ОпределитьУстановитьНастройкиТелефонии();
	КонецЕсли;
	Оповестить("ИзменениеСтатусаТелефонии");
	
КонецПроцедуры

&НаКлиенте
Процедура СтартТелефонии(Команда)
	УправлениеТелефониейКлиент.ВключитьТелефонию();	
КонецПроцедуры

&НаКлиенте
Процедура СтопТелефонии(Команда)
	УправлениеТелефониейКлиент.ВыключитьТелефонию();	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройки(Команда)
	
	СохранитьНастройкиНаСервере();
	Если Модифицированность Тогда
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаМенеджеру", ЭтаФорма);
		ПоказатьВопрос(Оповещение, "Внимание! Для корректной работы требуется перезапуск приложения. Перезапустить сейчас?", РежимДиалогаВопрос.ДаНет, 0); 		
	КонецЕсли;	
		
КонецПроцедуры

#КонецОбласти   

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработкаСтатусаТелефонии()
	
	ЭтаФорма.Загружена = глТелефония["Зарегистрирована"];  
	ЭтаФорма.Инициализирована = глТелефония["Активирована"];
	Элементы.ПускКомпоненты.Доступность = НЕ глТелефония["Зарегистрирована"];
	ТекущийСтатус = УправлениеТелефониейКлиент.ПолучитьСтатусТелефонии();
	Если ТекущийСтатус = "Есть подключение" Тогда 
		Элементы.НадписьСтатусТелефонии.Заголовок = ТекущийСтатус;
		Элементы.СтартТелефонии.Доступность = Ложь;
		Элементы.СтопТелефонии.Доступность = Истина;
	ИначеЕсли ТекущийСтатус = "Нет подключения" Тогда 
		Элементы.НадписьСтатусТелефонии.Заголовок = ТекущийСтатус;
		Элементы.СтартТелефонии.Доступность = Истина;
		Элементы.СтопТелефонии.Доступность = Ложь;
	КонецЕсли;
	
	Лог = ?(Не ПустаяСтрока(глТелефония["СтартСообщение"]), "СтартСообщение: " + глТелефония["СтартСообщение"], ?(Не ПустаяСтрока(глТелефония["СтопСообщение"]), "СтопСообщение: " + глТелефония["СтопСообщение"], ""));
	
КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьНастройкиТелефонии()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПользовательскиеНастройкиТелефонии.Пользователь КАК Пользователь,
	|	ПользовательскиеНастройкиТелефонии.Использование КАК Использование,
	|	ПользовательскиеНастройкиТелефонии.ТелефонияВключена КАК ТелефонияВключена,
	|	ПользовательскиеНастройкиТелефонии.АктивироватьЛог КАК АктивироватьЛог,
	|	ПользовательскиеНастройкиТелефонии.ТекущийНомерТелефона КАК ТекущийНомерТелефона,
	|	ПользовательскиеНастройкиТелефонии.Логин КАК Логин,
	|	ПользовательскиеНастройкиТелефонии.НеВыводитьВнутренниеЗвонки КАК НеВыводитьВнутренниеЗвонки,
	|	ПользовательскиеНастройкиТелефонии.КаталогСохраненияЛоговТелефонии КАК КаталогСохраненияЛоговТелефонии,
	|	ПользовательскиеНастройкиТелефонии.ДатаАктивации КАК ДатаАктивации
	|ИЗ
	|	РегистрСведений.ПользовательскиеНастройкиТелефонии КАК ПользовательскиеНастройкиТелефонии
	|ГДЕ
	|	ПользовательскиеНастройкиТелефонии.Пользователь = &Пользователь";
	
	Запрос.УстановитьПараметр("Пользователь", ПользователиКлиентСервер.АвторизованныйПользователь());
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда	
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();		
		Если ВыборкаДетальныеЗаписи.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(ЭтаФорма, ВыборкаДетальныеЗаписи);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиНаСервере()
	
	РеквизитыФормы = ЭтаФорма.ПолучитьРеквизиты();	
	Для каждого РеквизитФормы Из РеквизитыФормы Цикл
		Попытка
			СтароеЗначение = УправлениеТелефониейСервер.ПолучитьНастройкуТелефонииПользователя(РеквизитФормы.Имя);
			Если НЕ СтароеЗначение = ЭтаФорма[РеквизитФормы.Имя] Тогда
				УправлениеТелефониейСервер.УстановитьНастройкуТелефонииПользователя(РеквизитФормы.Имя, ЭтаФорма[РеквизитФормы.Имя]);
				Модифицированность = Истина;
			КонецЕсли;
		Исключение
			Продолжить;
		КонецПопытки;
	КонецЦикла;	
		
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаМенеджеру(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	УправлениеТелефониейКлиент.ПриЗавершенииРаботыСистемы();
	ЗавершитьРаботуСистемы(Ложь, Истина);
	
КонецПроцедуры

#КонецОбласти  

