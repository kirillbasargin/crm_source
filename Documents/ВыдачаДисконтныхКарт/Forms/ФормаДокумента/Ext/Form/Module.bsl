
&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Объект.НомерДисконтнойКарты = СтрЗаменить(Объект.НомерДисконтнойКарты, " ", "");
	
	ПараметрыЗаписи.Вставить("ЭтоНовый", Параметры.Ключ.Пустая());
	
	Если Не ЗначениеЗаполнено(Объект.НомерДисконтнойКарты) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не указан номер карты", Объект.Ссылка, "НомерДисконтнойКарты", "Объект", Отказ);
	КонецЕсли;		
	
	Если Не ЗначениеЗаполнено(Объект.МаркетинговаяКампания) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не указана маркетинговая кампания", Объект.Ссылка, "МаркетинговаяКампания", "Объект", Отказ);
	КонецЕсли;		
	
	Если Не ЗначениеЗаполнено(Объект.ДатаВыдачи) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не указана Дата выдачи", Объект.Ссылка, "ДатаВыдачи", "Объект", Отказ);
	КонецЕсли;			
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(ТекущийОбъект.Получатель) И (ПараметрыЗаписи.Свойство("ЭтоНовый") И ПараметрыЗаписи.ЭтоНовый) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаявкаНаСделкуДольщики.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ЗаявкаНаСделку.Дольщики КАК ЗаявкаНаСделкуДольщики
		|ГДЕ
		|	ЗаявкаНаСделкуДольщики.Клиент = &Клиент
		|	И ЗаявкаНаСделкуДольщики.Реальный";
		
		Запрос.УстановитьПараметр("Клиент", ТекущийОбъект.Получатель);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если РезультатЗапроса.Пустой() Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("По клиенту не заключено ни одной сделки, выдать карту невозможно", ТекущийОбъект.Ссылка, "Получатель", "Объект", Отказ);		
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МаркетинговаяКампанияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыОткрытияФормы = Новый Структура("ВыборИзДисконтнойКарты");	
	ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ПослеЗакрытияФормыВыбораМК", ЭтотОбъект);
	ОткрытьФорму("Справочник.МаркетинговыеКомпании.Форма.ФормаВыбораМК", ПараметрыОткрытияФормы, ЭтаФорма, , , , ОписаниеОповещенияОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияФормыВыбораМК(РезультатЗакрытия, ДополнительныеПараметры) Экспорт 
	
	Если РезультатЗакрытия = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.МаркетинговаяКампания = РезультатЗакрытия.МК;
	
КонецПроцедуры
