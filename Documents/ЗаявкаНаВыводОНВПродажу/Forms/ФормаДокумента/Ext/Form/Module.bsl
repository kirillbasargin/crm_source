
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;		
	
	НовыйДокумент = Параметры.Ключ.Пустая();
	
	Если НовыйДокумент Тогда
		Если Параметры.Свойство("ОбъектНедвижимости") Тогда
			Объект.ОбъектНедвижимости = Параметры.ОбъектНедвижимости;	
		КонецЕсли;
		Объект.Ответственный 	= ПараметрыСеанса.ТекущийПользователь;	
		Объект.Автор		 	= ПараметрыСеанса.ТекущийПользователь;
		Объект.Статус 			= Перечисления.СтатусЗаявкиНаВыводОНВПродажу.СогласованиеЗаявки;
		Объект.ТипОперации 		= Перечисления.ТипыОперацийЗаявкиНаВыводОНВПродажу.ВыводВПродажу;
		ОбновитьДанныеОбъектаНедвижимостиНаСервере();
	КонецЕсли; 	
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	ОбновитьДанныеОбъектаНедвижимостиНаСервере();
	
	СтатусДоЗаписи = Объект.Статус;
	
	Если Объект.Статус <> Перечисления.СтатусЗаявкиНаВыводОНВПродажу.СогласованиеЗаявки Тогда
	
		ЭтотОбъект.ТолькоПросмотр = Истина;	
		
		Элементы.ФормаСогласовать.Доступность = Ложь;
		Элементы.ФормаОтклонить.Доступность = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	//При записи нового документа
	Если НовыйДокумент Тогда
		НовыйДокумент = Ложь;
		ВыполнитьРассылкуНаСервере();
	КонецЕсли;	
	
	//Если изменился статут с согласования на тот, что отметил руководитель
	Если СтатусДоЗаписи = Перечисления.СтатусЗаявкиНаВыводОНВПродажу.СогласованиеЗаявки
			И (Объект.Статус = Перечисления.СтатусЗаявкиНаВыводОНВПродажу.СогласованаРуководителемФилиала
					ИЛИ Объект.Статус = Перечисления.СтатусЗаявкиНаВыводОНВПродажу.ОтклоненаРуководителемФилиала) Тогда
		СтатусДоЗаписи = Объект.Статус;
		ВыполнитьРассылкуНаСервере(Ложь);	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОбъектНедвижимостиПриИзменении(Элемент)
	
	ОбновитьДанныеОбъектаНедвижимостиНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Согласовать(Команда)
	
	СогласоватьНаСервере("СогласованаРуководителемФилиала");
	
КонецПроцедуры

&НаКлиенте
Процедура Отклонить(Команда)
	
	СогласоватьНаСервере("ОтклоненаРуководителемФилиала");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьДанныеОбъектаНедвижимостиНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтатусыОбъектовНедвижимостиСрезПоследних.Статус КАК СтатусОбъектаНедвижимости,
	|	Квартирограмма.Стоимость КАК Стоимость,
	|	Квартирограмма.ДатаСтартаПродаж КАК ДатаВыводаВПул,
	|	Квартирограмма.СтатусПула КАК СтатусПула
	|ИЗ
	|	РегистрСведений.Квартирограмма КАК Квартирограмма
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыОбъектовНедвижимости.СрезПоследних(, ОбъектНедвижимости = &ОбъектНедвижимости) КАК СтатусыОбъектовНедвижимостиСрезПоследних
	|		ПО Квартирограмма.ОбъектНедвижимости = СтатусыОбъектовНедвижимостиСрезПоследних.ОбъектНедвижимости
	|ГДЕ
	|	Квартирограмма.ОбъектНедвижимости = &ОбъектНедвижимости";
	
	Запрос.УстановитьПараметр("ОбъектНедвижимости", Объект.ОбъектНедвижимости);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(ЭтаФорма, Выборка);				
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура СогласоватьНаСервере(Знач Статус)
	
	Объект.Статус 				= Перечисления.СтатусЗаявкиНаВыводОНВПродажу[Статус];
	ЭтаФорма.Записать();
	ЭтаФорма.ТолькоПросмотр 	= Истина;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьРассылкуНаСервере(ДляРуководителя = Истина)

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОбъектыНедвижимости.Владелец.Проект.Руководитель КАК Руководитель,
	|	ПредставлениеСсылки(ОбъектыНедвижимости.Ссылка) КАК ОбъектНедвижимости,
	|	ПредставлениеСсылки(ОбъектыНедвижимости.Владелец) КАК ОбъектСтроительства,
	|	ПредставлениеСсылки(Клиенты.Ссылка) КАК Клиент,
	|	Клиенты.ОсновнойТелефон КАК Телефон
	|ИЗ
	|	Справочник.ОбъектыНедвижимости КАК ОбъектыНедвижимости,
	|	Справочник.Клиенты КАК Клиенты
	|ГДЕ
	|	ОбъектыНедвижимости.Ссылка = &ОН
	|	И Клиенты.Ссылка = &Клиент";
	Запрос.УстановитьПараметр("ОН", 	Объект.ОбъектНедвижимости);
	Запрос.УстановитьПараметр("Клиент", Объект.Клиент);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Если НЕ ЗначениеЗаполнено(Выборка.Руководитель) Тогда
			Возврат;
		КонецЕсли;
		
		СистемнаяУчетнаяЗаписьEMAIL = Справочники.УчетныеЗаписиЭлектроннойПочты.СистемнаяУчетнаяЗаписьЭлектроннойПочты;			
		EMAILАдрес 					= УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(?(ДляРуководителя, Выборка.Руководитель, Объект.Автор), 
																									Справочники.ВидыКонтактнойИнформации.EmailПользователя);
																									
																									
		ПараметрыСообщения = Новый Структура;
		ПараметрыСообщения.Вставить("Кому", EMAILАдрес);
																									
		Если ДляРуководителя Тогда 																									
			Тело = "Необходимо рассмотреть заявку на вывод в пул ";
			ПараметрыСообщения.Вставить("Тема", "Согласование на вывод ОН в продажу");
		Иначе
			Тело = ?(Объект.Статус = Перечисления.СтатусЗаявкиНаВыводОНВПродажу.СогласованаРуководителемФилиала, "Согласована руководителем", "Отклонена руководителем") 
						+ " заявка на вывод в пул ";
			ПараметрыСообщения.Вставить("Тема", ?(Объект.Статус = Перечисления.СтатусЗаявкиНаВыводОНВПродажу.СогласованаРуководителемФилиала, "Согласована руководителем", "Отклонена руководителем") + " заявка на вывод ОН в продажу");
		КонецЕсли;											
		
		Тело = Тело + Строка(Объект.Номер) + " от " + Формат(Объект.Дата, "ДФ=dd.MM.yyyy")
						+ " по ОН: " + Выборка.ОбъектНедвижимости  
						+ " " + Выборка.ОбъектСтроительства + "; " 
						+ " по клиенту "  
						+ Выборка.Клиент + " " + Выборка.Телефон;
		
		ПараметрыСообщения.Вставить("Тело", Тело);
		
		Попытка
			РаботаСПочтовымиСообщениями.ОтправитьПочтовоеСообщение(СистемнаяУчетнаяЗаписьEMAIL, ПараметрыСообщения);
		Исключение
			//ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
