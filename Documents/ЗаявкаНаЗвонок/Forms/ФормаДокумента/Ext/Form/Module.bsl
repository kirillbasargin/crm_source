
#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ВерсионированиеОбъектов") Тогда
		МодульВерсионированиеОбъектов = ОбщегоНазначения.ОбщийМодуль("ВерсионированиеОбъектов");
		МодульВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	КонецЕсли;
	
	Если Параметры.Ключ.Пустая() Тогда
		ЗаполнитьЗначенияСвойств(Объект, Параметры.ЗначенияЗаполнения);	
	КонецЕсли;
	
	Взаимодействия.ЗаполнитьСписокВыбораДляРассмотретьПосле(Элементы.РассмотретьПосле.СписокВыбора);

	ПриСозданииИПриЧтенииНаСервере();
	
	УстановитьДоступностьЭлементов();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ПриСозданииИПриЧтенииНаСервере();		
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Объект.reqContact = УправлениеТелефониейКлиентСервер.ФорматироватьТелефон(Объект.reqContact);
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ТекущийОбъект.ПлановоеВремя = РассмотретьПосле;			
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТекущийОбъект.ДополнительныеСвойства.Свойство("ВстречаДляОткрытия") И НЕ ТекущийОбъект.ДополнительныеСвойства.ВстречаДляОткрытия = Неопределено Тогда
		ПараметрыЗаписи.Вставить("ВстречаДляОткрытия", ТекущийОбъект.ДополнительныеСвойства.ВстречаДляОткрытия);
		ТекущийОбъект.ДополнительныеСвойства.Удалить("ВстречаДляОткрытия");		
	КонецЕсли;
	
	Если ТекущийОбъект.ДополнительныеСвойства.Свойство("ЗаявкиПоТекущемуНомеру") И НЕ ТекущийОбъект.ДополнительныеСвойства.ЗаявкиПоТекущемуНомеру = Неопределено Тогда
		ПараметрыЗаписи.Вставить("ЗаявкиПоТекущемуНомеру", ТекущийОбъект.ДополнительныеСвойства.ЗаявкиПоТекущемуНомеру);
		ТекущийОбъект.ДополнительныеСвойства.Удалить("ЗаявкиПоТекущемуНомеру");		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)	
	
	Оповестить("ЗаявкаНаЗвонокИзменена", Объект.Ссылка, ЭтаФорма);	
	
	Если ПараметрыЗаписи.Свойство("ВстречаДляОткрытия") И НЕ ПараметрыЗаписи.ВстречаДляОткрытия = Неопределено Тогда
		ОткрытьФорму("Документ.Встреча.Форма.ФормаДокумента", Новый Структура("Ключ", ПараметрыЗаписи.ВстречаДляОткрытия));
		ПараметрыЗаписи.Удалить("ВстречаДляОткрытия");		
	КонецЕсли;
	
	Если ПараметрыЗаписи.Свойство("ЗаявкиПоТекущемуНомеру") И НЕ ПараметрыЗаписи.ЗаявкиПоТекущемуНомеру = Неопределено Тогда
		Если ПараметрыЗаписи.ЗаявкиПоТекущемуНомеру.Количество() > 1 Тогда
			Сообщить("Перейдите в рабочий стол");	
		КонецЕсли;	
		ПараметрыЗаписи.Удалить("ЗаявкиПоТекущемуНомеру");		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если Источник = "Телефония" Тогда
		Если ИмяСобытия = "ИсходящийЗвонок" И Параметр.ЗаявкаИнициатор = Объект.Ссылка Тогда
			Прочитать();
			ОбновитьКоличествоПопыток();
		ИначеЕсли (ИмяСобытия = "НачалоРазговора" ИЛИ ИмяСобытия = "ОкончаниеЗвонка") И Параметр.ЗаявкаИнициатор = Объект.Ссылка Тогда
			Прочитать();
		КонецЕсли;
		Оповестить("ЗаявкаНаЗвонокИзменена", Объект.Ссылка, ЭтаФорма);
	КонецЕсли;	
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура reqContactПриИзменении(Элемент)
	reqContactПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СайтПриИзменении(Элемент)
	СайтПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПроектПриИзменении(Элемент)
	ПроектПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура КонтактПриИзменении(Элемент)
	КонтактПриИзмененииНаСервере();	
КонецПроцедуры

&НаКлиенте
Процедура РассмотретьПослеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ВзаимодействияКлиент.ОбработатьВыборВПолеРассмотретьПосле(
		РассмотретьПосле, ВыбранноеЗначение, СтандартнаяОбработка, Модифицированность);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЭтоМенеджер Тогда
		СтандартнаяОбработка = Ложь;	
		СписокВыбора = Новый СписокЗначений;
		СписокВыбора.Добавить(ПредопределенноеЗначение("Перечисление.СтатусЗаявкиCallBack.БылиПопыткиДозвониться"));
		Если Параметры.Ключ.Пустая() Тогда
			СписокВыбора.Добавить(ПредопределенноеЗначение("Перечисление.СтатусЗаявкиCallBack.НеОбработано"));	
		КонецЕсли;
		ДанныеВыбора = СписокВыбора;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РассмотретьПослеПриИзменении(Элемент)
	
	//ТекДата = ТекущаяДата();
	//Д = Лев(ТекДата, 2);
	//М = Сред(ТекДата, 4, 2);
	//Г = Сред(ТекДата, 7, 4);
	//ТекДата = Формат(Дата(Г, М, Д), "ДФ=dd.MM.yyyy");
	//
	////РассмотретьПосле =  ;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветственныйПриИзменении(Элемент)
	
	Если Объект.ТипВызова = ПредопределенноеЗначение("Перечисление.ТипыВызовов.УстнаяЗаявка") 
		И Объект.ВидЗаявкиНаЗвонок = ПредопределенноеЗначение("Перечисление.ВидыЗаявокНаЗвонок.СозданаВручную") Тогда
		Объект.ДатаРаспределения = ТекущаяДата();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПопытокДозвонитьсяПриИзменении(Элемент)	
	//ПопытокДозвонитьсяПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПриоритетПриИзменении(Элемент)
	УстановитьДоступностьЭлементов();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Позвонить(Команда)
	
	Если Параметры.Ключ.Пустая() ИЛИ Модифицированность Тогда		
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросОЗаписиЗаявки", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, "Заявка не записана. Записать заявку?", РежимДиалогаВопрос.ДаНет);
	Иначе
		Если Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусЗаявкиCallBack.Обработано") Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Заявка уже обработана");
			Возврат;	
		КонецЕсли;
		Если глТелефония["Активирована"] Тогда
			глТелефония["ЗаявкаИнициатор"] = Объект.Ссылка;
			УправлениеТелефониейКлиент.СделатьЗвонок(УправлениеТелефониейКлиентСервер.ФорматироватьТелефон(Объект.reqContact));
			УправлениеТелефониейКлиентПовтИсп.АктивизироватьНачальнуюСтраницу();
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЗвонки(Команда)
	
	УсловияОтбора = Новый Структура;
	УсловияОтбора.Вставить("ЗаявкаИнициатор", Объект.Ссылка);
	//УсловияОтбора.Вставить("Ответственный", Объект.Ответственный);
	ПараметрыФормы = Новый Структура("Отбор", УсловияОтбора);
	ОткрытьФорму("РегистрСведений.ИсторияЗвонков.ФормаСписка", ПараметрыФормы);			
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВопросОЗаписиЗаявки(Ответ, Контекст) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Запись));
	КонецЕсли;
	
КонецПроцедуры
		
&НаСервере
Процедура ПриСозданииИПриЧтенииНаСервере()
	
	РассмотретьПосле = Объект.ПлановоеВремя;
	ОбновитьКоличествоПопыток();
	
КонецПроцедуры
		
&НаСервере
Процедура ОбновитьКоличествоПопыток()
	
	ПопытокДозвониться = Объект.ПопытокДозвониться;
	Если Объект.ПопытокДозвониться Тогда
		Элементы.ФормаОткрытьЗвонки.Заголовок = "Открыть звонки" + "(" + ПопытокДозвониться + ")";
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура reqContactПриИзмененииНаСервере()
	
	Объект.reqContact = УправлениеТелефониейКлиентСервер.ФорматироватьТелефон(Объект.reqContact);
	Объект.Контакт = УправлениеТелефониейСервер.ОпределитьКлиента(Объект.reqContact);	
	
КонецПроцедуры

&НаСервере
Процедура СайтПриИзмененииНаСервере()
	
	Объект.Проект = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Сайт, "ОбъектНедвижимости");
	Объект.ВидНедвижимости = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Проект, "ВидНедвижимости");
	
КонецПроцедуры

&НаСервере
Процедура ПроектПриИзмененииНаСервере()
	Объект.ВидНедвижимости = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Проект, "ВидНедвижимости");
КонецПроцедуры

&НаСервере
Процедура КонтактПриИзмененииНаСервере()
	Объект.reqContact = УправлениеТелефониейКлиентСервер.ФорматироватьТелефон(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Контакт, "ОсновнойТелефон"));
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьЭлементов()
	
	МенеджерГПТ = УправлениеДоступом.ЕстьРоль("ОбработкаЗаявокНаЗвонок_Менеджер", ?(Параметры.Ключ.Пустая(), Неопределено, Объект.Ссылка), Пользователи.АвторизованныйПользователь()); //РольДоступна("ОбработкаЗаявокНаЗвонок_Менеджер");
	СтаршийСмены = УправлениеДоступом.ЕстьРоль("ОбработкаЗаявокНаЗвонок_Администратор", ?(Параметры.Ключ.Пустая(), Неопределено, Объект.Ссылка), Пользователи.АвторизованныйПользователь()); //РольДоступна("ОбработкаЗаявокНаЗвонок_Администратор");
	ЭтоМенеджер = МенеджерГПТ И НЕ СтаршийСмены;
	
	Если ЭтоМенеджер Тогда
		Элементы.СтраницаСлужебные.Видимость = Ложь;
		Элементы.reqData.ТолькоПросмотр = ЭтоМенеджер;
		Элементы.reqEmail.ТолькоПросмотр = ЭтоМенеджер;
		Элементы.Тема.ТолькоПросмотр = ЭтоМенеджер И НЕ Объект.ВидЗаявкиНаЗвонок = Перечисления.ВидыЗаявокНаЗвонок.СозданаВручную;
		Элементы.ВидЗаявкиНаЗвонок.ТолькоПросмотр = ЭтоМенеджер;
		//Элементы.ТипВызова.ТолькоПросмотр = ЭтоМенеджер;
		Элементы.ТипПоступленияЗаявки.ТолькоПросмотр = ЭтоМенеджер;
		Элементы.ЭтапРаботы.ТолькоПросмотр = ЭтоМенеджер;
		Элементы.ДатаПоследнейЗагрузки.ТолькоПросмотр = ЭтоМенеджер;	
		Элементы.ГруппаРаспределения.ТолькоПросмотр = ЭтоМенеджер;
		//<>, Басаргин (29.03.2018) {
		Элементы.ТипРаспределения.ТолькоПросмотр = ЭтоМенеджер;
		Элементы.СтатусПланированияВстречи.ТолькоПросмотр = ЭтоМенеджер;
		//<> }		
		Элементы.Сайт.ТолькоПросмотр = ЭтоМенеджер;
		Элементы.Проект.ТолькоПросмотр = ЭтоМенеджер И НЕ Объект.ВидЗаявкиНаЗвонок = Перечисления.ВидыЗаявокНаЗвонок.СозданаВручную;
		Элементы.Запрос.ТолькоПросмотр = ЭтоМенеджер;
		Элементы.Взаимодействие.ТолькоПросмотр = ЭтоМенеджер;
		Элементы.ПопытокДозвониться.ТолькоПросмотр = ЭтоМенеджер;
		Элементы.ПопытокCегодня.ТолькоПросмотр = ЭтоМенеджер;
		Элементы.ДеньОбработки.ТолькоПросмотр = ЭтоМенеджер;	
		Элементы.Ответственный.ТолькоПросмотр = ЭтоМенеджер И НЕ Объект.ВидЗаявкиНаЗвонок = Перечисления.ВидыЗаявокНаЗвонок.СозданаВручную;
		Элементы.СобытиеCQ.ТолькоПросмотр = ЭтоМенеджер;
		Элементы.ДатаРаспределения.ТолькоПросмотр = ЭтоМенеджер; // И НЕ Объект.Приоритет = Перечисления.ПриоритетыЗаявокНаЗвонок.Приоритет3;
		
		Элементы.ПолеДополнительнойГруппировки.Видимость = Ложь;
		Элементы.reqType.Видимость = Ложь;
		Элементы.UCID.Видимость = Ложь;
		Элементы.reqID.Видимость = Ложь;
		Элементы.reqStatus.Видимость = Ложь;
		Элементы.Комментарии.Видимость = Ложь;
		
		//Если Объект.ТипВызова = Перечисления.ТипыВызовов.УстнаяЗаявка
		//	ИЛИ Объект.ВидЗаявкиНаЗвонок = Перечисления.ВидыЗаявокНаЗвонок.СозданаВручную Тогда
		//	Элементы.ДатаРаспределения.ТолькоПросмотр = Ложь;
		//Иначе
		//	Элементы.ДатаРаспределения.ТолькоПросмотр = ЭтоМенеджер;
		//КонецЕсли;
		
		//СписокВыбора = Новый Массив;
		//СписокВыбора.Добавить(Перечисления.СтатусЗаявкиCallBack.БылиПопыткиДозвониться);
		//Если Параметры.Ключ.Пустая() Тогда
		//	СписокВыбора.Добавить(Перечисления.СтатусЗаявкиCallBack.НеОбработано)	
		//КонецЕсли;
		// Элементы.Статус.СписокВыбора.ЗагрузитьЗначения(СписокВыбора); //+режим выбора из списка
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПопытокДозвонитьсяПриИзмененииНаСервере()
	
	Если Объект.ПопытокДозвониться >= 5
		И Объект.Статус = Перечисления.СтатусЗаявкиCallBack.БылиПопыткиДозвониться
		И ((НачалоДня(ТекущаяДата()) - НачалоДня(Объект.reqData)) / (60 * 60 * 24)) >= 1  
		И Объект.ДатаРаспределения <= ТекущаяДата() Тогда			
		Если Объект.Приоритет = Перечисления.ПриоритетыЗаявокНаЗвонок.Приоритет3 Тогда
			Объект.Приоритет = Перечисления.ПриоритетыЗаявокНаЗвонок.Приоритет4;
			Объект.Статус = Перечисления.СтатусЗаявкиCallBack.НеОбработано;
			Объект.Ответственный = Неопределено;
			Объект.ДатаРаспределения = НачалоДня(ТекущаяДата()) + 60 * 60 * 24;
		Иначе					
			Объект.Статус = Перечисления.СтатусЗаявкиCallBack.НеДозвон;
		КонецЕсли;
	Иначе
		Объект.Статус = Перечисления.СтатусЗаявкиCallBack.БылиПопыткиДозвониться;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
