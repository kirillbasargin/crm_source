
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ВерсионированиеОбъектов") Тогда
		МодульВерсионированиеОбъектов = ОбщегоНазначения.ОбщийМодуль("ВерсионированиеОбъектов");
		МодульВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
КонецПроцедуры

//<853881>, Басаргин (10.08.2018) {
&НаСервере
Процедура ОбновитьНаСервере()	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Ссылка", ПолучитьНаправленияНаконсультацию(Пользователи.ТекущийПользователь()), ВидСравненияКомпоновкиДанных.ВСписке, , Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	ОбновитьНаСервере();
КонецПроцедуры

&НаСервереБезконтекста
Функция ПолучитьНаправленияНаконсультацию(ТекущийПользователь)
	
	МассивСсылок = Новый Массив;
	
	ПроектыПользователя = Новый Массив;
	ГруппыПользователя = РегистрыСведений.Взаимодействия.ПолучитьГруппуПользователяВторогоУровня(ТекущийПользователь); //РегистрыСведений.Взаимодействия.ПолучитьГруппуОтветственного(Пользователи.АвторизованныйПользователь(), ТекущаяДата(), Неопределено);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	СоответствиеГруппПользователейОфисам.Проект КАК Проект
	|ИЗ
	|	РегистрСведений.СоответствиеГруппПользователейОфисам КАК СоответствиеГруппПользователейОфисам
	|ГДЕ
	|	СоответствиеГруппПользователейОфисам.ГруппаПользователей В(&ГруппыПользователя)";
	
	Запрос.УстановитьПараметр("ГруппыПользователя", ГруппыПользователя);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда		
		ПроектыПользователя = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Проект");
	КонецЕсли;
	
	Если ПроектыПользователя.Количество() Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РасчетИпотеки.Ссылка КАК Ссылка,
		|	РасчетИпотеки.Запрос КАК Запрос
		|ПОМЕСТИТЬ ВТ_РасчетыИпотеки
		|ИЗ
		|	Документ.РасчетИпотеки КАК РасчетИпотеки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставыГруппПользователейИсторияИзмений КАК СоставыГруппПользователейИсторияИзмений
		|		ПО РасчетИпотеки.Ответственный = СоставыГруппПользователейИсторияИзмений.Пользователь
		|			И (СоставыГруппПользователейИсторияИзмений.ГруппаПользователей.Наименование = ""Филиал"")
		|			И (ВЫБОР
		|				КОГДА СоставыГруппПользователейИсторияИзмений.ДатаВыходаИзГруппы = ДАТАВРЕМЯ(1, 1, 1)
		|					ТОГДА РасчетИпотеки.Дата >= СоставыГруппПользователейИсторияИзмений.ДатаВключенияВГруппу
		|				ИНАЧЕ РасчетИпотеки.Дата >= СоставыГруппПользователейИсторияИзмений.ДатаВключенияВГруппу
		|						И РасчетИпотеки.Дата < СоставыГруппПользователейИсторияИзмений.ДатаВыходаИзГруппы
		|			КОНЕЦ)
		|ГДЕ
		|	РасчетИпотеки.ЖК В(&Проекты)
		|	И НЕ РасчетИпотеки.ПометкаУдаления
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Запрос
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Взаимодействия.Предмет КАК Запрос
		|ПОМЕСТИТЬ ВТ_Взаимодействия
		|ИЗ
		|	РегистрСведений.Взаимодействия КАК Взаимодействия
		|ГДЕ
		|	Взаимодействия.ГруппаОтветственного = &ГруппаОтветственного
		|	И Взаимодействия.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыВзаимодействий.Завершено), ЗНАЧЕНИЕ(Перечисление.СтатусыВзаимодействий.Отменено))
		|	И Взаимодействия.Предмет В
		|			(ВЫБРАТЬ
		|				ВТ_РасчетыИпотеки.Запрос КАК Запрос
		|			ИЗ
		|				ВТ_РасчетыИпотеки КАК ВТ_РасчетыИпотеки)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Запрос
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_РасчетыИпотеки.Ссылка КАК Ссылка
		|ИЗ
		|	ВТ_РасчетыИпотеки КАК ВТ_РасчетыИпотеки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Взаимодействия КАК ВТ_Взаимодействия
		|		ПО ВТ_РасчетыИпотеки.Запрос = ВТ_Взаимодействия.Запрос
		|ГДЕ
		|	ВТ_Взаимодействия.Запрос ЕСТЬ NULL";
		
		Запрос.УстановитьПараметр("ТекущийПользователь", ТекущийПользователь);
		Запрос.УстановитьПараметр("Проекты", ПроектыПользователя);
		Запрос.УстановитьПараметр("ГруппаОтветственного", Справочники.ГруппыПользователей.НайтиПоНаименованию("Ипотечные брокеры"));
		
		Результат = Запрос.Выполнить();
	
		Если Результат.Пустой() Тогда
			Возврат МассивСсылок;
		Иначе			
			МассивСсылок = Результат.Выгрузить().ВыгрузитьКолонку("Ссылка");
		КонецЕсли;
	КонецЕсли;

	Возврат МассивСсылок;
	
КонецФункции
//<853881> }
