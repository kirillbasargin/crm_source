
&НаКлиенте
Процедура Сформировать(Команда)
	
	СформироватьНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьНаСервере()
	
	ТД.Очистить();
	ТДПрисоединения = Новый ТабличныйДокумент;
	
	ДатаОтчета = КонецДня(Дата);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ИпотечныеСтавкиСрезПоследних.СхемаУсловийСтавок КАК СхемаУсловийСтавок,
		|	ИпотечныеСтавкиСрезПоследних.ПрограммаКредитования КАК ПрограммаКредитования,
		|	ИпотечныеСтавкиСрезПоследних.Банк КАК Банк
		|ИЗ
		|	РегистрСведений.ИпотечныеСтавки.СрезПоследних(
		|			&Дата,
		|			Банк = &Банк
		|				ИЛИ &Банк = ЗНАЧЕНИЕ(Справочник.Банки.ПустаяСсылка)) КАК ИпотечныеСтавкиСрезПоследних
		|ГДЕ
		|	ИпотечныеСтавкиСрезПоследних.Активен
		|ИТОГИ ПО
		|	Банк,
		|	ПрограммаКредитования,
		|	СхемаУсловийСтавок";
	
	Запрос.УстановитьПараметр("Банк", Банк);
	Запрос.УстановитьПараметр("Дата", ДатаОтчета);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаБанк = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Макет = Отчеты.МультикалькуляторПроцентныеСтавки.ПолучитьМакет("Макет");
	
	
	ЧередованиеЦветов = Ложь;
	
	Пока ВыборкаБанк.Следующий() Цикл
		ПостфиксОбласти = ?(ЧередованиеЦветов, "1","");
		
		ОбластьБанк = Макет.ПолучитьОбласть("ОбластьБанк"+ПостфиксОбласти);
		ОбластьПрограмма = Макет.ПолучитьОбласть("ОбластьПрограмма"+ПостфиксОбласти);
		ОбластьСхема = Макет.ПолучитьОбласть("ОбластьСхема"+ПостфиксОбласти);
	
		ОбластьБанк.Параметры.Банк = ВыборкаБанк.Банк;
		ТД.Вывести(ОбластьБанк);
		ВыборкаПрограмма = ВыборкаБанк.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПрограмма.Следующий() Цикл
			ОбластьПрограмма.Параметры.Программа = ВыборкаПрограмма.ПрограммаКредитования;
			ТД.Вывести(ОбластьПрограмма);
			ВыборкаСхема = ВыборкаПрограмма.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаСхема.Следующий() Цикл
				ОбластьСхема.Параметры.Схема = ВыборкаСхема.СхемаУсловийСтавок;
				ТД.Вывести(ОбластьСхема);
				Расположения = Неопределено;
				Документы.УсловияПримененияСтавок.ЗаполнитьТабДокПоСхеме(ВыборкаСхема.СхемаУсловийСтавок,ТДПрисоединения,Расположения);
				Документы.УсловияПримененияСтавок.ОтобразитьЗначенияВТабДоке(ВыборкаСхема.СхемаУсловийСтавок,ТДПрисоединения, Расположения,ДатаОтчета);
				ТД.Вывести(ТДПрисоединения);
			КонецЦикла;
		КонецЦикла;
		ЧередованиеЦветов = НЕ ЧередованиеЦветов;
	КонецЦикла;

	
КонецПроцедуры