
&НаКлиенте
Процедура ИзменитьДату(Команда)
	
	ПараметрыФормы = Новый Структура("НачальноеЗначение", Период);
	ОткрытьФорму("ОбщаяФорма.ВыборДаты", ПараметрыФормы, Период, ЭтаФорма,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	Период = НачалоМесяца(Период);
КонецПроцедуры

&НаСервере
Функция СформироватьНаСервере(НачалоМесяца, КонецМесяца, Проект = Неопределено)
	
	ТаблицаВстреч = ПолучитьСписокВстреч(НачалоМесяца, Проект);
	
	ТД.Очистить();
	ОтчетОбъект = РеквизитФормыВЗначение("Отчет");
	ТабДокумент = Новый ТабличныйДокумент;
	
	Макет = ОтчетОбъект.ПолучитьМакет("Макет");
	Область = Макет.ПолучитьОбласть("Шапка");
	Область.Параметры.ДатаОтчета = ПредставлениеПериода(НачалоМесяца, КонецМесяца);
	ТД.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть("ЗаголовокДни");
	ТД.Вывести(Область);
	
	КоличествоНедель = 5;
	
	ОбластьДат    = Макет.ПолучитьОбласть("Даты");
	ОбластьСтроки = Макет.ПолучитьОбласть("Строки");
	
	НакопленноеКоличествоСтрок = 0;
	ОбрабатываемаяДата = НачалоМесяца;
	
	Для СчетчикНедель = 1 По КоличествоНедель Цикл
		
		Если ТаблицаВстреч = Неопределено Тогда
			ДатаНачалаНедели 	= НачалоНедели(ОбрабатываемаяДата);
			ДатаОкончанияНедели = КонецНедели(ОбрабатываемаяДата);
		Иначе
			//Вывод дат
			СписокДат = ТаблицаВстреч.Скопировать(Новый Структура("НомерНедели", СчетчикНедель));
			СписокДат.Свернуть("НачалоПериода, КонецПериода");
			
			ДатаНачалаНедели 	= СписокДат[0].НачалоПериода;
			ДатаОкончанияНедели = СписокДат[0].КонецПериода;
		КонецЕсли;
		
		ОбрабатываемаяДата = ДатаНачалаНедели;
		
		Для СчетчикДней = 1 По 7 Цикл
			Если СчетчикНедель = 1 И СчетчикДней = 1 Тогда
				ФорматнаяСтрока = ?(ОбрабатываемаяДата = НачалоДня(НачалоМесяца), "ДФ='dd'", "ДФ='dd MMMM'");
			Иначе
				Если ОбрабатываемаяДата = КонецДня(КонецМесяца) + 1 Тогда
					ФорматнаяСтрока = "ДФ='dd MMMM'";
				Иначе
					ФорматнаяСтрока = ?(ОбрабатываемаяДата = НачалоДня(НачалоМесяца) Или ОбрабатываемаяДата = НачалоДня(КонецМесяца), "ДФ='dd MMMM'", "ДФ='dd'");
				КонецЕсли;
			КонецЕсли;
			
			ОбластьДат.Параметры["ДатаМесяца" + СчетчикДней] = Формат(ОбрабатываемаяДата, ФорматнаяСтрока);
			ОбрабатываемаяДата = КонецДня(ОбрабатываемаяДата) + 1;
		КонецЦикла;
		
		ТД.Вывести(ОбластьДат);
		
		//Вывод строк
		Если ТаблицаВстреч = Неопределено Тогда
			КоличествоСтрок = 7;
		Иначе
			СписокСтрок = ТаблицаВстреч.Скопировать(Новый Структура("НомерНедели", СчетчикНедель));
			СписокСтрок.Свернуть("ДеньНедели", "Количество");
			
			КоличествоСтрок = 1;
			Для Каждого СтрСписка Из СписокСтрок Цикл
				КоличествоСтрок = ?(КоличествоСтрок > СтрСписка.Количество, КоличествоСтрок, СтрСписка.Количество);
			КонецЦикла;
			
			КоличествоСтрок = ?(КоличествоСтрок >= 7, КоличествоСтрок, 7);
			СписокСтрок = ТаблицаВстреч.Скопировать(Новый Структура("НомерНедели", СчетчикНедель));
			СписокСтрок.Сортировать("ДеньНедели");
			
			НоваяТаблица = СписокСтрок.Скопировать();
			НоваяТаблица.Очистить();
		КонецЕсли;
		
		Для КолСтрок = 1 По КоличествоСтрок Цикл
			Для СчетчикДней = 1 По 7 Цикл
				Если ТаблицаВстреч = Неопределено Тогда
					СписокСтрокДня = Новый Массив;
				Иначе
					СписокСтрокДня = СписокСтрок.Скопировать(Новый Структура("ДеньНедели", СчетчикДней));
				КонецЕсли;
				
				Если СписокСтрокДня.Количество() = 0 Тогда
					ОбластьСтроки.Параметры["ВремяВстречи" + СчетчикДней] = " - ";
					ОбластьСтроки.Параметры["РасшифровкаВремяВстречи" + СчетчикДней] = Неопределено;
					ОбластьСтроки.Параметры["КартинкаВремяВстречи" + СчетчикДней] = Неопределено;
				ИначеЕсли СписокСтрокДня.Количество() = 1 Тогда
					ТекСтрока = СписокСтрокДня[0];
					Часы = Строка(ТекСтрока.Час);
					Минуты = Строка(ТекСтрока.Минута);
					ОбластьСтроки.Параметры["ВремяВстречи" + СчетчикДней] = ?(СтрДлина(Часы) = 1, "0" + Часы, Часы) + ":" + ?(СтрДлина(Минуты) = 1, "0" + Минуты, Минуты) + " - " + СокрЛП(Строка(ТекСтрока.ВзаимодействиеОтветственный));
					ОбластьСтроки.Параметры["РасшифровкаВремяВстречи" + СчетчикДней] = ТекСтрока.Взаимодействие;
					Если ТекСтрока.Рассмотрено Тогда
						ОбластьСтроки.Параметры["КартинкаВремяВстречи" + СчетчикДней] = БиблиотекаКартинок.Успешно;
					ИначеЕсли ТекСтрока.Взаимодействие.Отменено Тогда
						ОбластьСтроки.Параметры["КартинкаВремяВстречи" + СчетчикДней] = БиблиотекаКартинок.Остановить;
					Иначе
						ОбластьСтроки.Параметры["КартинкаВремяВстречи" + СчетчикДней] = БиблиотекаКартинок.СтартБизнесПроцесса;
					КонецЕсли;
				Иначе
					Для Каждого СтрСписка Из СписокСтрокДня Цикл
						Если СписокСтрокДня.Индекс(СтрСписка) = 0 Тогда
							Часы = Строка(СтрСписка.Час);
							Минуты = Строка(СтрСписка.Минута);
							ОбластьСтроки.Параметры["ВремяВстречи" + СчетчикДней] = ?(СтрДлина(Часы) = 1, "0" + Часы, Часы) + ":" + ?(СтрДлина(Минуты) = 1, "0" + Минуты, Минуты) + " - " + СокрЛП(Строка(СтрСписка.ВзаимодействиеОтветственный));
							ОбластьСтроки.Параметры["РасшифровкаВремяВстречи" + СчетчикДней] = СтрСписка.Взаимодействие;
							Если СтрСписка.Рассмотрено Тогда
								ОбластьСтроки.Параметры["КартинкаВремяВстречи" + СчетчикДней] = БиблиотекаКартинок.Успешно;
							ИначеЕсли СтрСписка.Взаимодействие.Отменено Тогда
								ОбластьСтроки.Параметры["КартинкаВремяВстречи" + СчетчикДней] = БиблиотекаКартинок.Остановить;
							Иначе
								ОбластьСтроки.Параметры["КартинкаВремяВстречи" + СчетчикДней] = БиблиотекаКартинок.СтартБизнесПроцесса;
							КонецЕсли;
						Иначе
							ЗаполнитьЗначенияСвойств(НоваяТаблица.Добавить(), СтрСписка);
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
			
			ТД.Вывести(ОбластьСтроки);
			
			Если ТаблицаВстреч <> Неопределено Тогда
				СписокСтрок = НоваяТаблица.Скопировать();
				НоваяТаблица.Очистить();
			КонецЕсли;
		КонецЦикла;
		
		//Объединение
		СтрокаНачало 	= 5 + НакопленноеКоличествоСтрок + СчетчикНедель;
		СтрокаОкончание = 5 + НакопленноеКоличествоСтрок + КоличествоСтрок + СчетчикНедель - 1;
		
		ТД.Область(СтрокаНачало, 1, СтрокаОкончание, 2).Объединить();
		ТД.Область(СтрокаНачало, 1, СтрокаОкончание, 2).ОриентацияТекста = 90;
		ТД.Область(СтрокаНачало, 1, СтрокаОкончание, 2).ВертикальноеПоложение = ВертикальноеПоложение.Центр;
		ТД.Область(СтрокаНачало, 1, СтрокаОкончание, 2).ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
		ТД.Область(СтрокаНачало, 1, СтрокаОкончание, 2).ГраницаСнизу = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 2);
		
		Если Месяц(ДатаНачалаНедели) = Месяц(ДатаОкончанияНедели) Тогда
			ТекстСтроки = Формат(ДатаНачалаНедели, "ДФ='dd'") + " - " + Формат(ДатаОкончанияНедели, "ДФ='dd.MM'");
		Иначе
			ТекстСтроки = Формат(ДатаНачалаНедели, "ДФ='dd.MM'") + " - " + Формат(ДатаОкончанияНедели, "ДФ='dd.MM'");
		КонецЕсли;
		
		ТД.Область(СтрокаНачало, 1, СтрокаОкончание, 2).Текст = ТекстСтроки;
		
		НакопленноеКоличествоСтрок = НакопленноеКоличествоСтрок + КоличествоСтрок;
	КонецЦикла;
	
	Область = Макет.ПолучитьОбласть("Окончание");
	ТД.Вывести(Область);
	
	Возврат ТД;
	
КонецФункции

&НаКлиенте
Процедура Сформировать(Команда)
	
	ТД = СформироватьНаСервере(НачалоМесяца(Период), КонецМесяца(Период));
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Свойство("Период") Тогда
		Период = НачалоМесяца(ТекущаяДата());
	Иначе
		Если ЗначениеЗаполнено(Параметры.Период) Тогда
			Период = НачалоМесяца(Параметры.Период);
		Иначе
			Период = НачалоМесяца(ТекущаяДата());
		КонецЕсли;
	КонецЕсли;
	
	Проект = Справочники.Проекты.ПустаяСсылка();
	Если Параметры.Свойство("Предмет") И ЗначениеЗаполнено(Параметры.Предмет) Тогда
		Если ТипЗнч(Параметры.Предмет) = Тип("ДокументСсылка.Запрос") Тогда
			Проект = Параметры.Предмет.Проект;
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Свойство("СформироватьПриОткрытии") И Параметры.СформироватьПриОткрытии <> Неопределено И Параметры.СформироватьПриОткрытии Тогда
		ТД = СформироватьНаСервере(НачалоМесяца(Период), КонецМесяца(Период), Проект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСписокВстреч(Период, Проект)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(&НачалоПериода, НЕДЕЛЯ) КАК НачалоПериода,
		|	КОНЕЦПЕРИОДА(&НачалоПериода, НЕДЕЛЯ) КАК КонецПериода,
		|	1 КАК НомерНедели
		|ПОМЕСТИТЬ ВТ_Периоды
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(&НачалоПериода, НЕДЕЛЯ, 1), НЕДЕЛЯ),
		|	КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(&НачалоПериода, НЕДЕЛЯ, 1), НЕДЕЛЯ),
		|	2
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(&НачалоПериода, НЕДЕЛЯ, 2), НЕДЕЛЯ),
		|	КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(&НачалоПериода, НЕДЕЛЯ, 2), НЕДЕЛЯ),
		|	3
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(&НачалоПериода, НЕДЕЛЯ, 3), НЕДЕЛЯ),
		|	КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(&НачалоПериода, НЕДЕЛЯ, 3), НЕДЕЛЯ),
		|	4
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(&НачалоПериода, НЕДЕЛЯ, 4), НЕДЕЛЯ),
		|	КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(&НачалоПериода, НЕДЕЛЯ, 4), НЕДЕЛЯ),
		|	5
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ГруппыПользователей.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ВТ_Группы
		|ИЗ
		|	Справочник.ГруппыПользователей КАК ГруппыПользователей
		|ГДЕ
		|	ГруппыПользователей.Наименование = ""Земля розница""
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СоставыГруппПользователейИсторияИзменийСрезПоследних.Пользователь КАК Пользователь
		|ПОМЕСТИТЬ ВТ_Пользователи
		|ИЗ
		|	РегистрСведений.СоставыГруппПользователейИсторияИзмений.СрезПоследних(
		|			&Период,
		|			ГруппаПользователей В ИЕРАРХИИ
		|				(ВЫБРАТЬ
		|					ВТ_ГРУППЫ.Ссылка
		|				ИЗ
		|					ВТ_ГРУППЫ)) КАК СоставыГруппПользователейИсторияИзменийСрезПоследних
		|ГДЕ
		|	СоставыГруппПользователейИсторияИзменийСрезПоследних.ДатаВключенияВГруппу < &Период
		|	И (СоставыГруппПользователейИсторияИзменийСрезПоследних.ДатаВыходаИзГруппы = ДАТАВРЕМЯ(1, 1, 1)
		|			ИЛИ СоставыГруппПользователейИсторияИзменийСрезПоследних.ДатаВыходаИзГруппы >= &НачалоПериода)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ПредметыПапкиВзаимодействий.Взаимодействие КАК Взаимодействие,
		|	ПредметыПапкиВзаимодействий.РассмотретьПосле КАК РассмотретьПосле,
		|	ПредметыПапкиВзаимодействий.Взаимодействие.Ответственный КАК ВзаимодействиеОтветственный,
		|	ДЕНЬНЕДЕЛИ(ПредметыПапкиВзаимодействий.РассмотретьПосле) КАК ДеньНедели,
		|	ДЕНЬ(ПредметыПапкиВзаимодействий.РассмотретьПосле) КАК День,
		|	НЕДЕЛЯ(ПредметыПапкиВзаимодействий.РассмотретьПосле) КАК Неделя,
		|	ЧАС(ПредметыПапкиВзаимодействий.РассмотретьПосле) КАК Час,
		|	МИНУТА(ПредметыПапкиВзаимодействий.РассмотретьПосле) КАК Минута,
		|	ПредметыПапкиВзаимодействий.Рассмотрено КАК Рассмотрено,
		|	ЕСТЬNULL(Взаимодействия.Проект, ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)) КАК Проект
		|ПОМЕСТИТЬ ВТ_Взаимодействия
		|ИЗ
		|	РегистрСведений.ПредметыПапкиВзаимодействий КАК ПредметыПапкиВзаимодействий
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Взаимодействия КАК Взаимодействия
		|		ПО ПредметыПапкиВзаимодействий.Взаимодействие = Взаимодействия.Взаимодействие
		|ГДЕ
		|	ТИПЗНАЧЕНИЯ(ПредметыПапкиВзаимодействий.Взаимодействие) = ТИП(Документ.Встреча)
		|	И ПредметыПапкиВзаимодействий.Взаимодействие.Ответственный В
		|			(ВЫБРАТЬ
		|				ВТ_Пользователи.Пользователь
		|			ИЗ
		|				ВТ_Пользователи)
		|	И ПредметыПапкиВзаимодействий.РассмотретьПосле МЕЖДУ НАЧАЛОПЕРИОДА(&НачалоПериода, НЕДЕЛЯ) И ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&НачалоПериода, НЕДЕЛЯ), МЕСЯЦ, 1)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Взаимодействия.Взаимодействие КАК Взаимодействие,
		|	ВТ_Взаимодействия.РассмотретьПосле КАК РассмотретьПосле,
		|	ВТ_Взаимодействия.ВзаимодействиеОтветственный КАК ВзаимодействиеОтветственный,
		|	ВТ_Взаимодействия.ДеньНедели КАК ДеньНедели,
		|	ВТ_Взаимодействия.День КАК День,
		|	ВТ_Взаимодействия.Неделя КАК Неделя,
		|	ВТ_Взаимодействия.Час КАК Час,
		|	ВТ_Взаимодействия.Минута КАК Минута,
		|	ВТ_Периоды.НачалоПериода КАК НачалоПериода,
		|	ВТ_Периоды.КонецПериода КАК КонецПериода,
		|	ВТ_Периоды.НомерНедели КАК НомерНедели,
		|	1 КАК Количество,
		|	ВТ_Взаимодействия.Рассмотрено КАК Рассмотрено
		|ИЗ
		|	ВТ_Периоды КАК ВТ_Периоды
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Взаимодействия КАК ВТ_Взаимодействия
		|		ПО (ВТ_Взаимодействия.РассмотретьПосле МЕЖДУ ВТ_Периоды.НачалоПериода И ВТ_Периоды.КонецПериода)
		|ГДЕ
		|	ВТ_Взаимодействия.Проект = &Проект";
	
	//Запрос.УстановитьПараметр("Ответственный", Пользователь); //Пользователи.ТекущийПользователь());
	Запрос.УстановитьПараметр("НачалоПериода", Период);
	Запрос.УстановитьПараметр("Период", ДобавитьМесяц(Период, 1));
	Если Проект = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТ_Взаимодействия.Проект = &Проект", "ИСТИНА");
	Иначе
		Запрос.УстановитьПараметр("Проект", Проект);
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	Иначе
		Возврат Результат.Выгрузить();
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ТДВыбор(Элемент, Область, СтандартнаяОбработка)
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура ПериодСтрокойПриИзменении(Элемент)
	
	Если ПериодСтрокой <> "" Тогда
		Если СтрДлина(ПериодСтрокой)=4 Тогда 
			СформироватьСписокВыбораМесяца(Число(ПериодСтрокой));
			Период = НачалоМесяца(Дата(Число(ПериодСтрокой), 1, 1));
			ПериодСтрокой = Формат(Период, "ДФ='MMMM yyyy'");
		Иначе
			НомМесяца = (Найти("янвфевмарапрмайиюниюлавгсеноктноядек", Нрег(Лев(ПериодСтрокой,3)))+2)/3; //получаем номер месяца
			ВыбрГод = Число(Прав(ПериодСтрокой, 4));
			Период = НачалоМесяца(Дата(ВыбрГод, НомМесяца, 1));
		КонецЕсли;    
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьСписокВыбораМесяца(Год)
	
	Элементы.ПериодСтрокой.СписокВыбора.Очистить();
	Элементы.ПериодСтрокой.СписокВыбора.Добавить(Формат(Год-1, "ЧГ=0"));
	Для к = 1 По 12  Цикл
		СформДата = Дата(Год, к, 1);
		Наим = Формат(СформДата, "ДФ = ММММ_гггг");
		Наим = СтрЗаменить(Наим, "_", " ");
		Элементы.ПериодСтрокой.СписокВыбора.Добавить(Наим);    
	КонецЦикла;
	
	Элементы.ПериодСтрокой.СписокВыбора.Добавить(Формат(Год+1, "ЧГ=0"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПериодСтрокой = Формат(Период, "ДФ='MMMM yyyy'");
	Если Год(Период) = 1 Тогда
		СформироватьСписокВыбораМесяца(Год(ТекущаяДата()));
	Иначе
		СформироватьСписокВыбораМесяца(Год(Период));
	КонецЕсли;

КонецПроцедуры

