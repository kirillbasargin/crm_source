#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВариантыОтчетов

// См. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.
//
Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
	
	МодульВариантыОтчетов = ОбщегоНазначения.ОбщийМодуль("ВариантыОтчетов");
	МодульВариантыОтчетов.УстановитьРежимВыводаВПанеляхОтчетов(Настройки, НастройкиОтчета, Ложь);
	
	НастройкиОтчета.ОпределитьНастройкиФормы = Истина;
	НастройкиОтчета.Размещение.Вставить(ВариантыОтчетовКлиентСервер.ИдентификаторНачальнойСтраницы(), "Важный");
	
	НастройкиВарианта_Горизонталь = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "Основной");
	НастройкиВарианта_Горизонталь.Описание = НСтр("ru = 'Отчет о движениях документа (Горизонтально).'");
	НастройкиВарианта_Горизонталь.НастройкиДляПоиска.КлючевыеСлова = НСтр("ru = 'Отчет о движениях документа'");
	
	НастройкиВарианта_Вертикаль = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "Дополнительный");
	НастройкиВарианта_Вертикаль.Описание = НСтр("ru = 'Отчет о движениях документа (Вертикально).'");
	НастройкиВарианта_Вертикаль.НастройкиДляПоиска.КлючевыеСлова = НСтр("ru = 'Отчет о движениях документа'");
	
КонецПроцедуры

// Для вызова из процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.
// 
// Параметры:
//   КомандыОтчетов              - ТаблицаЗначений - Таблица команд для вывода в подменю.
//                                                   См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.
//   Параметры                   - Структура - Структура, содержащая параметры подключения команды.
//   ДокументыСОтчетомОДвижениях - Массив - Массив документов, в которых будет выводится команда открытия отчета.
//
// Возвращаемое значение:
//   СтрокаТаблицыЗначений, Неопределено - добавленная команда или Неопределено, если нет прав на просмотр отчета.
//
Функция ДобавитьКомандуОтчетОДвиженияхДокумента(КомандыОтчетов, Параметры, ДокументыСОтчетомОДвижениях) Экспорт
	
	Если Не ПравоДоступа("Просмотр", Метаданные.Отчеты.ОтчетОДвиженияхДокумента) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Параметры.ВидВРег <> "ДОКУМЕНТ" Тогда
		Возврат Неопределено;
	Иначе
		
		ОтключитьОтчетОтДокументов(КомандыОтчетов);
		
		РазделенноеИмяФормы = СтрРазделить(Параметры.ИмяФормы, ".");
		Если РазделенноеИмяФормы.Количество() > 2 Тогда
			
			ИмяДокумента       = РазделенноеИмяФормы.Получить(1);
			ДокументМетаданные = Метаданные.Документы.Найти(ИмяДокумента);
			Если ДокументыСОтчетомОДвижениях.Найти(ДокументМетаданные) = Неопределено Тогда
				Возврат Неопределено;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Команда                    = КомандыОтчетов.Добавить();
	Команда.Представление      = НСтр("ru = 'Отчет о движениях документа'");
	Команда.МножественныйВыбор = Ложь;
	Команда.ИмяПараметраФормы  = "";
	Команда.Важность           = "СмТакже";
	Команда.КлючВарианта       = "Основной";
	Команда.Менеджер           = "Отчет.ОтчетОДвиженияхДокумента";
	
	Возврат Команда;
	
КонецФункции

// См. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.
Процедура ПриНастройкеВариантовОтчетов(Настройки) Экспорт
	
	ВариантыОтчетов.НастроитьОтчетВМодулеМенеджера(Настройки, Метаданные.Отчеты.ОтчетОДвиженияхДокумента);
	ВариантыОтчетов.ОписаниеОтчета(Настройки, Метаданные.Отчеты.ОтчетОДвиженияхДокумента).Включен = Ложь;
	
КонецПроцедуры

// См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.
Процедура ПередДобавлениемКомандОтчетов(КомандыОтчетов, НастройкиФормы, СтандартнаяОбработка) Экспорт
	
	ДокументыСОтчетомОДвижениях = СформироватьДокументыСОтчетомОДвижениях(КомандыОтчетов);
	
	ДобавитьКомандуОтчетОДвиженияхДокумента(КомандыОтчетов, НастройкиФормы, ДокументыСОтчетомОДвижениях);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВариантыОтчетов

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СформироватьДокументыСОтчетомОДвижениях(КомандыОтчетов, ДокументыСОтчетомОДвижениях = Неопределено)
	
	ДокументыСОтчетомОДвижениях = Новый Массив;
	МетаданныеДокументы         = Метаданные.Документы;
	Для Каждого МетаданныеДокумент Из МетаданныеДокументы Цикл
		Если МетаданныеДокумент.Движения.Количество() > 0 Тогда
			ДокументыСОтчетомОДвижениях.Добавить(МетаданныеДокумент);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ДокументыСОтчетомОДвижениях;
	
КонецФункции

Процедура ОтключитьОтчетОтДокументов(КомандыОтчетов)
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("Менеджер", "Отчет.ОтчетОДвиженияхДокумента");
	НайденныеСтроки = КомандыОтчетов.НайтиСтроки(СтруктураПоиска);
	
	Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
		КомандыОтчетов.Удалить(НайденнаяСтрока);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
